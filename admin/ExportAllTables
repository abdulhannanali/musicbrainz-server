#!/home/httpd/musicbrainz/mb_server/cgi-bin/perl -w
# vi: set ts=4 sw=4 :

#____________________________________________________________________________
#
#   MusicBrainz -- the open internet music database
#
#   Copyright (C) 1998 Robert Kaye
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#   $Id$
#____________________________________________________________________________

use FindBin;
use lib "$FindBin::Bin/../cgi-bin";

use strict;
use DBI;
use DBDefs;
use integer;

use Getopt::Long;

my $fHelp;
my $OutputDir = ".";
my $dir = "/tmp";
my $fCompress = 1;
my $fKeepFiles = 0;
my $fProgress = -t STDOUT;
my @tablelist;

GetOptions(
	"output-dir|d=s"	=> \$OutputDir,
	"tmp-dir|t=s"		=> \$dir,
	"compress|c!"		=> \$fCompress,
	"keep-files|k!"		=> \$fKeepFiles,
	"table=s"			=> \@tablelist,
	"help"				=> \$fHelp,
);

sub usage
{
	print <<EOF;
Usage: ExportAllTables [options]

        --help            show this help
    -d, --output-dir DIR  place the final archive files in DIR (default: ".")
    -t, --tmp-dir DIR     use DIR for temporary storage (default: /tmp)
    -c, --[no]compress    [don't] create .tar.bz2 archives after exporting
    -k, --keep-files      don't delete the exported files from the tmp directory
        --table TABLE     process only these tables

Specifying --nocompress and omitting --keep-files would discard the
files just exported straight away, so don't do that.  If you specify --table
TABLE, you won't get a complete consistent snapshot of the database, of
course.

EOF
}

usage(), exit if $fHelp;
usage(), exit 1 if @ARGV;
usage(), exit 1 if not $fCompress and not $fKeepFiles;

use File::Temp qw( tempdir );
$SIG{'INT'} = sub { exit 3 };
$dir = tempdir("mbexport-XXXXXX", DIR => $dir, CLEANUP => not $fKeepFiles);
mkdir "$dir/mbdump" or die $!;
print "Exporting to $dir/mbdump\n" if $fKeepFiles;

use MusicBrainz;
my $mb = new MusicBrainz;
$mb->Login;

use Sql;
my $sql = Sql->new($mb->{DBH});
my $dbh = $mb->{DBH};

my @tables = qw(
	album
	album_amazon_asin
	album_amazon_asin_sanitised
	albumjoin
	albummeta
	albumwords
	artist
	artist_relation
	artistalias
	artistwords
	clientversion
	country
	currentstat
	discid
	historicalstat
	moderation_closed
	moderation_note_closed
	moderation_note_open
	moderation_open
	moderator
	moderator_preference
	moderator_subscribe_artist
	moderator_sanitised
	release
	stats
	toc
	track
	trackwords
	trm
	trmjoin
	vote_closed
	vote_open
	wordlist
);

@tables = @tablelist if @tablelist;

use Time::HiRes qw( gettimeofday tv_interval );
my $t0 = [gettimeofday];
my $totalrows = 0;
my $tables = 0;

$sql->AutoCommit;
$sql->Do("SET SESSION CHARACTERISTICS AS TRANSACTION ISOLATION LEVEL SERIALIZABLE");
$sql->Begin;

my $now = $sql->SelectSingleValue("SELECT NOW()");
open(TIMESTAMP, ">$dir/mbdump/TIMESTAMP") or die $!;
print TIMESTAMP "This snapshot was taken at $now UTC\n";
close TIMESTAMP;

$sql->Do("SELECT * INTO TEMPORARY TABLE album_amazon_asin_sanitised FROM album_amazon_asin");
$sql->Do("UPDATE album_amazon_asin_sanitised SET coverarturl = ''");

$sql->Do("SELECT * INTO TEMPORARY TABLE moderator_sanitised FROM moderator");
$sql->Do("UPDATE moderator_sanitised SET password = 'mb', privs = 0, email = ''");

$| = 1;

printf "%-30.30s %9s %4s %9s\n",
	"Table", "Rows", "est%", "rows/sec",
	;

for my $table (@tables)
{
	open(DUMP, ">$dir/mbdump/$table")
		or die $!;

	my $estrows = $sql->SelectSingleValue(
		"SELECT reltuples FROM pg_class WHERE relname = ? LIMIT 1",
		$table,
	) || 1;

	$sql->Do("COPY $table TO stdout");
	my $buffer;
	my $rows = 0;

	my $t1 = [gettimeofday];
	my $interval;

	my $p = sub {
		my ($pre, $post) = @_;
		no integer;
		printf $pre."%-30.30s %9d %3d%% %9d".$post,
			$table, $rows, int(100 * $rows / $estrows),
			$rows / ($interval||1);
	};

	$p->("", "") if $fProgress;

	# This has to be at least as large as the longest line given
	# by "copy table to stdout".
	# Currently the largest value I can see is a row in the moderation
	# table which is approx 215,000 bytes.
	my $max = 250_000; my $maxline = __LINE__;

	while ($dbh->func($buffer, $max, "getline"))
	{
		die "\nProbable data truncation!  See $0 line $maxline"
			if length($buffer) >= $max-1;

		print DUMP $buffer, "\n"
			or die $!;

		++$rows;
		unless ($rows & 0xFFF)
		{
			$interval = tv_interval($t1);
			$p->("\r", "") if $fProgress;
		}
	}

	$dbh->func("endcopy") or die;
	close DUMP
		or die $!;

	$interval = tv_interval($t1);
	$p->(($fProgress ? "\r" : ""), sprintf(" %.2f sec\n", $interval));

	++$tables;
	$totalrows += $rows;
}

$sql->Commit;
my $dumptime = tv_interval($t0);
printf "Dumped %d tables (%d rows) in %d seconds\n",
	$tables, $totalrows, $dumptime;

# Now we have all the files; disconnect from the database.
# This also drops the _sanitised temporary tables.
undef $sql;
undef $mb;

my @group_core = qw(
	album
	albumjoin
	artist
	artistalias
    clientversion
	country
	discid
	release
	toc
	track
	trm
	trmjoin
	);

my @group_derived = qw(
	album_amazon_asin_sanitised
	albummeta
	albumwords
	artistwords
	trackwords
	wordlist
	);

my @group_moderation = qw(
	currentstat
	historicalstat
	moderation_note_open
	moderation_open
	moderator_sanitised
	stats
	vote_open
	);

my @group_closedmoderation = qw(
	moderation_closed
	moderation_note_closed
	vote_closed
	);

my @group_artistrelation = qw(
	artist_relation
);

my @group_modprivate = qw(
	album_amazon_asin
	moderator
	moderator_preference
	moderator_subscribe_artist
);

use File::Copy qw( copy );

if ($fCompress)
{
	copy("$FindBin::Bin/COPYING-PublicDomain", "$dir/mbdump/COPYING") or die $!;
	make_tar("mbdump.tar.bz2", @group_core);

	copy("$FindBin::Bin/COPYING-CCShareAlike", "$dir/mbdump/COPYING") or die $!;
	make_tar("mbdump-derived.tar.bz2", @group_derived);
	make_tar("mbdump-moderation.tar.bz2",  @group_moderation);
	make_tar("mbdump-closedmoderation.tar.bz2",  @group_closedmoderation);
	make_tar("mbdump-artistrelation.tar.bz2", @group_artistrelation);
	make_tar("mbdump-moderator.tar.bz2", @group_modprivate);
}

sub make_tar
{
	my ($tarfile, @files) = @_;

	unshift @files, "TIMESTAMP", "COPYING";

	my $t0 = [gettimeofday];
	print "Creating $tarfile\n";
	system { "/bin/tar" } "tar",
		"-C", $dir,
		"--bzip2",
		"--create",
		"--verbose",
		"--file", "$OutputDir/$tarfile",
		"--",
		map { "mbdump/$_" } @files,
		;
	$? == 0 or warn "Tar returned $?";
	printf "Tar completed in %d seconds\n", tv_interval($t0);
}

# eof ExportAllTables

#The GNUPLOT statistics file
#
# use 'gnuplot <this file name>'
#
# to create two graphs indicating the growth of MusicBrainz
#  - stats_MB.gif   shown the number of albums, moderations, and TRMIds in time
#  - stats_all.gif  shown the number of albums for MB, FreeDB, CDDB, AllMusic, and
#                   Muze in time.
#                   
# Created in Dec 2001 by Johan Pouwelse

GraceNoteDups=3
FreeDBDups=2

set terminal gif
set xdata time
set timefmt "%d %m %Y"
set xlabel "Time"
set xrange ["01 10 2000" : "1 1 2003"]
set format x "%m/%Y"
set key left
set ylabel "Number of entries in MusicBrainz"

set output "stats_MB.gif"

plot "musicbrainz.dat" using 1:($5) title "Albums" with linespoints, \
     "musicbrainz.dat"    using 1:($7) title "Moderations" with linespoints, \
     "musicbrainz.dat"    using 1:($8) title "TRM Ids" with linespoints

set logscale y 2
#set xdata time
#set timefmt "%d %m %Y"
#set xlabel "Time"
set xrange ["01 01 1997" : "1 1 2003"]
#set format x "%m/%Y"
#set xtics "01 01 1997", 3600*24*30.5*6, "31 12 2002"
set ylabel "number of albums, using LOGSCALE ( x 1000)"

set output "stats_all.gif"

# the plots are almost equal, with scaling 1 more datapoint
plot "musicbrainz.dat" using 1:($5/1000) title "MusicBrainz" with linespoints, \
     "cddb.dat"        using 1:($4/GraceNoteDups/1000) title "GraceNote" with linespoints, \
     "allmusic.dat"    using 1:($5/1000) title "AllMusic" with linespoints, \
     "freedb.dat"      using 1:($4/FreeDBDups/1000) title "FreeDB" with linespoints, \
     "muze.dat"        using 1:($5/1000) title "Muze" with points pointtype 2 pointsize 4


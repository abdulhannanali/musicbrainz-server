%# vi: set ts=2 sw=2 ft=mason :
<%args>
$name => ""
$sortname => ""
$type => 0
$resolution => ""
$begindate_y => ""
$begindate_m => ""
$begindate_d => ""
$enddate_y => ""
$enddate_m => ""
$enddate_d => ""
$notetext => ""
$initial_name => ""
$confirm => ""
</%args>
<%perl>


MusicBrainz::IsSingleLineString($name)
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsSingleLineString($sortname)
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsSingleLineString($resolution)
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($type) && Artist::IsValidType($type)
	or return $m->comp("/comp/badargs", 1, 1);

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

my $mb = $m->comp("/comp/dblogin");

# Try to load the named artist by name. If an artist comes up, give the user a stern warning
# about adding a duplicate artist.
my @dupes;

if ($name ne "")
{
		my $ar = Artist->new($mb->{DBH});
	  my $artists = $ar->GetArtistsFromName($name);

		my $norm_name = MusicBrainz::NormaliseSortText($name);
		@dupes = grep {
			MusicBrainz::NormaliseSortText($_->GetName) eq $norm_name
		} @$artists;
}

# If anything has been filled in, the user has clicked 'submit'.
# We only accept valid dates or no dates at all.
if ($name =~ m/\S/ 
		and (not(@dupes) or ($resolution ne "" and $confirm ne ""))
	  and MusicBrainz::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d)
	  and MusicBrainz::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d)
	  and MusicBrainz::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d,
	  																		$enddate_y, $enddate_m, $enddate_d)
  	and Artist::IsValidType($type) )
{
  # If the artist is not deemed to be the same as another artist, don't save the comment
  $resolution = "" if not @dupes;

	my $ret = $m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_ADD_ARTIST,
				# --
				name => $name,
				sortname => $sortname,
				artist_type => $type,
				artist_resolution => $resolution,
				artist_begindate => [ $begindate_y, $begindate_m, $begindate_d ],
				artist_enddate => [ $enddate_y, $enddate_m, $enddate_d ],
			);

			$mods[0]->InsertNote($session{'uid'}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;

			# Return the mods inserted - see below
			@mods;
		},
	) or return;

	my $url = "/search.html";
	(my $addmod) = grep { $_->Type eq &ModDefs::MOD_ADD_ARTIST } @$ret;
	$url = "/showartist.html?artistid=" . $addmod->GetRowId
		if $addmod;
	$m->comp("/comp/redirect", $url);

	die "Shouldn't get here";
}

my $error_msg;
unless (
		MusicBrainz::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d)
	and MusicBrainz::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d)
	and MusicBrainz::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d,
																			$enddate_y, $enddate_m, $enddate_d) )
{
	$error_msg = 'Please enter valid dates';
}

</%perl>

<& /comp/sidebar, title => 'Add Artist' &>

<& /comp/stylemenu &>

% if (defined $error_msg) {
	<p style="color: red">
		<% $error_msg %>
	</p>
% }

<p>
	You are about to add an artist to MusicBrainz.&nbsp;
</p>

<%def .ShowArtistLink>
<a href="/artist/<% $_[0]->GetMBId %>.html"><% $_[0]->GetName %></a>\
</%def>

% if (@dupes) {
	<div class="similarartist">
		<span class="warning">Warning!</span> You are attempting to add the 
		artist "<% $name %>" to MusicBrainz, which appears to be very similar, 
		if not exactly the same as
% if (@dupes == 1) {
		the <span id="AddArtistExistingList">existing artist called <& .ShowArtistLink, $dupes[0] &>.
% } else {
		these <span id="AddArtistExistingList">existing artists called
		<%perl>
			for my $i (0..$#dupes) {
				$m->out(", ") if ($i != 0 && $i < $#dupes);
				$m->out("and ") if ($i == $#dupes);
				$m->comp(".ShowArtistLink", $dupes[$i]);
			}
		</%perl>.
% }
		</span>
		<p>If you are certain that you want to add this artist, then:</p>
		<ul>
			<li>You <b>must</b> provide a comment which allows to distinguish 
			this new artist from
% if (@dupes == 1) {
			the other artist called <& .ShowArtistLink, $dupes[0] &>;
% } else {
			the other similarly-named artists;
% }
			</li>
			<li>You must also check the <b>confirm</b> checkbox below.</li>
		</ul>
	</div>
% }

<& /mod/edit-artist/main-form,
		action			=> '/addartist.html',
		name				=> $name || $initial_name,
		sortname		=> $sortname,
		type				=> $type,
		resolution	=> $resolution,
		begindate		=> [ $begindate_y, $begindate_m, $begindate_d ],
		enddate			=> [ $enddate_y, $enddate_m, $enddate_d ],
		notetext		=> $notetext,
		confirm     => (@dupes > 0),
		showres     => (@dupes > 0),
&>

<& /comp/footer &>

%# vi: set ts=2 sw=2 ft=mason :
<%args>
$artistid => undef
$numtracks => 1
@trackname => ()
$confirm => 0
</%args>
<%perl>

$m->comp("/comp/checkloggedin")
	or return;

MusicBrainz::IsNonNegInteger($numtracks)
	&& $numtracks >= 1
	&& $numtracks <= 100
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($artistid) && $artistid
	or return $m->comp("/comp/badargs", 1, 1);

$artistid == ModDefs::VARTIST_ID
	and return $m->comp("/comp/badargs", 1, 1);
$artistid == ModDefs::DARTIST_ID
	and return $m->comp("/comp/badargs", 1, 1);

my $mb = $m->comp("/comp/dblogin");

my $artist = Artist->new($mb->{DBH});
$artist->SetId($artistid);
$artist->LoadFromId()
	or return $m->comp(
		"/comp/error",
		"Artist not found in the database.",
		1, 1,
	);

my @names = grep /\S/, @trackname;
$numtracks = @names if $numtracks < @names;

if (@names and $confirm)
{
	my $ret = $m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			for my $name (@names)
			{
				Moderation->InsertModeration(
					DBH	=> $mb->{DBH},
					uid	=> $session{uid},
					privs => $session{privs},
					type => &ModDefs::MOD_ADD_TRACK_KV,
					# --
					artist => $artist,
					trackname => $name,
				);
			}
		},
	) or return;

	my $al = Album->new($mb->{DBH});
	$al->GetOrInsertNonAlbum($artistid);
	my $url = "/showalbum.html?albumid=" . $al->GetId;
	$m->comp("/comp/redirect", $url);

	die "Shouldn't get here";
}

for (@names[0..$numtracks-1])
{
	$_ = "" unless defined;
}

</%perl>

<& /comp/sidebar, title => "Add Non-Album Tracks" &>

<p>
	Please enter the name(s) of the track(s) you wish to
	add as non-album tracks of
	<b><% $artist->GetName %></b>.&nbsp;
	Just leave a track name blank if you don't want to
	add it.
</p>

% my $nextnum = 5 * int( ($numtracks + 5) / 5);
<p>
	<a href="/addnonalbumtracks.html?artistid=<% $artistid %>&amp;numtracks=<% $nextnum %>"
		>(Want to add more tracks than this?)</a>
</p>

<& /comp/stylemenu &>

<form method="post" action="/addnonalbumtracks.html"
	name="AddAlbumTracks"
	enctype="application/x-www-form-urlencoded"
	class="formstyle">

	<table>
		<tr>
			<td align="right">
				Artist:
			</td>
			<td>
				<% $artist->GetName() %>
			</td>
		</tr>
% for (1 .. $numtracks) {
		<tr>
			<td align="right">
				Track Name:
			</td>
			<td>
				<input type="text" name="trackname" size="40" value="<% $names[$_-1] %>">
			</td>
		</tr>
% }
		<tr>
			<td>
				&nbsp;
			</td>
			<td>
				<input type="hidden" name="artistid" value="<% $artistid %>">
				<input type="hidden" name="confirm" value="1">
				<input type="submit" value="Submit">
			</td>
		</tr>
	</table>

</form>

<& /comp/movefocus, "AddAlbumTracks", "trackname", ($numtracks>1 ? 0 : undef) &>
<& /comp/footer &>

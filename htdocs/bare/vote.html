<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Register the votes submitted from the voting page into the database.
	#
	# $Id$
	#
</%perl>
<%args>

	$url

</%args>
<%perl>

	# make sure we have a logged in user.
	$m->comp("/comp/checkloggedin", 1, 1) or return;

	my %votemap = (
		"yes"	=> &ModDefs::VOTE_YES,
		"no"	=> &ModDefs::VOTE_NO,
		"abs"	=> &ModDefs::VOTE_ABS,
	);

	# build votes hash from the rowid{editid} parameters,
	# and notes hash from the rowid{editid}-notetext parameters.
	my (%votes, %notes);
	while (my ($key, $value) = each %ARGS)
	{
		my $editid;
		if (($editid) = $key =~ /^rowid(\d+)$/)
		{
			my $vote = $votemap{$value};
			$votes{$editid} = $vote
				if defined $vote;
		} 
		elsif (($editid) = $key =~ /^rowid(\d+)-notetext$/)
		{
			$notes{$editid} = $value
				if $value =~ /\S/;
		}
	}
	%votes or %notes or return $m->comp("/comp/redirect", $url);

	my $mb = $m->comp("/comp/dblogin");
	my $sql = Sql->new($mb->{DBH});

	if (%votes)
	{
		my $v = MusicBrainz::Server::Vote->new($mb->{DBH});
		eval
		{
			$sql->Begin;
			$v->InsertVotes(\%votes, $session{uid});
			$sql->Commit;
		};
	}

	if (%notes)
	{
		my $n = MusicBrainz::Server::ModerationNote->new($mb->{DBH});
		eval
		{
			$sql->Begin;
			while (my ($editid, $note) = each %notes)
			{
				$n->Insert($editid, $session{uid}, $note);
			}
			$sql->Commit;
		};
	}

	if ($@)
	{
		my $err = $@;
		$sql->Rollback;


		$m->comp("/comp/sidebar", title => "Vote could not be added.");
		$m->out("<b>A database error occurred.</b>");
		$m->out("<pre>($err)</pre>");
		$m->comp("/comp/footer");
		return undef;
	}

	$mb->Logout();
	return $m->comp("/comp/redirect", $url);

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

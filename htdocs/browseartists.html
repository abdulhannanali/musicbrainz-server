%# vi: set ts=4 sw=2 ft=mason :
<%args>
	$index => ""
	$offset => undef
	$page => undef
</%args>
<%perl>
	$m->comp("/comp/sidebar-notitle", pagetitle => "Browse Artists");


	# always use the uppercase index
	$index = uc($index);

	# handle offset argument
	my $max_items = 50;
	$offset = ($page-1) * $max_items if not defined $offset and defined $page;
	$offset = 0 if not defined $offset;
	$offset = 0 if $offset < 0;

	# browse UI, wrapped in a box.
	$m->comp("/comp/tablebegin", title => "Browse Artists");
	$m->comp("/comp/browsebox", index => $index);
	$m->comp("/comp/tableend");

	# stop here, if we have nothing to search for.
	return if ($index eq "");

	# load album list from the database.
	my $mb = $m->comp("/comp/dblogin");
	my $num_artists = 0;
	my @info;
	my $ar = Artist->new($mb->{DBH});
	($num_artists, @info) = $ar->GetArtistDisplayList($index, $offset);

	# render results-box
	my $boxtitle = sprintf "Found %d Artists starting with \"%s\"", $num_artists, $index;
	$m->out(qq!<a name="results"></a> !);
	$m->comp("/comp/tablebegin",
		title => $boxtitle, showmodnotice => 1
	);

	# if no artists are found, suggest entering a new one.
	if ($num_artists == 0)
	{
		$index =~ s/_/?/g;
		$index =~ s/ /_/g;

		$m->out(qq!<div style="margin-bottom: 10px">If you have not found the artist you were are looking for, please add it to the database.</div>!);
		$m->out(qq![ <a href="/edit/artist/add.html">Add New Artist</a> ]!);
	}
	else
	{
		# page-navigation
    	$m->comp("/comp/browse/pageselector",
			numitems => $num_artists,
			numlinks => 6,
			snaptoend => 2,
			pagesize => $max_items,
			offset => $offset,
			url => "/browseartists.html",
			args => { index => $index }
		);

		$m->out(qq!<table class="listing" style="margin: 10px 0px">!);

		# render results in two columns
		my $rows = $max_items / 2;
		for(my $i = 0; $i < $rows; $i++)
		{
			last if (not defined $info[$i]); #exit condition

			# add header, if this is the first iteration
			if ($i == 0)
			{
				$m->out(qq!<tr class="header">!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!</tr>!);
			}

			my $row = 0;
			$m->out(qq!<tr>!);
			for ($info[$i], $info[$i + $rows])
			{
				if (not defined $_)
				{
					$m->out(qq!<td colspan="2">&nbsp;</td>!);
					last;
				}
				else
				{
					$m->out(sprintf qq!<td class="browsenr">%d.</td>!, $i + 1 + ($row++*$rows) + $offset);
					$m->out(sprintf qq!<td class="browsename%s">!, ($$_[2] ? " modpending" : ""));
					$m->out(sprintf qq!<a href="/showartist.html?artistid=%d">%s</a></td>!,
						$$_[0],
						encode_entities($$_[1]));
				}
			}
			$m->out(qq!</tr>!);
		}
		$m->out(qq!</table>!);


    	$m->comp("/comp/browse/pageselector",
			numitems => $num_artists,
			numlinks => 6,
			snaptoend => 2,
			pagesize => $max_items,
			offset => $offset,
			url => "/browseartists.html",
			args => { index => $index }
		);
	}
	$m->comp("/comp/tableend");
	$mb->Logout;

</%perl>
<& /comp/footer &>

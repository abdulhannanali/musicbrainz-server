<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Browse the releases.
	#
	# $Id$
	#
</%perl>
<%args>

	$reltype => undef
	$relstatus => undef
	$artists => ""

	$index => ""
	$offset => undef
	$page => undef

</%args>



<%def .DescribeAlbumMeta>
<%perl>
	my $al = shift;
	my $msg = sprintf "Artist: %s - %d track%s - %d discid%s - %d PUID%s",
		$al->{artistname},
		$al->GetTrackCount, ($al->GetTrackCount == 1 ? "" : "s"),
		$al->GetDiscidCount, ($al->GetDiscidCount == 1 ? "" : "s"),
		$al->GetPuidCount, ($al->GetPuidCount == 1 ? "" : "s");
	$m->out($msg);
</%perl>
</%def>



<%perl>
	$m->comp("/comp/sidebar-notitle", pagetitle => "Browse Releases");

	# always use the uppercase index
	$index = uc($index);

	# handle release type argument
	MusicBrainz::Server::Validation::IsNonNegInteger($reltype) or $reltype = 0;
	$reltype >= MusicBrainz::Server::Release::RELEASE_ATTR_SECTION_TYPE_START or $reltype = 0;
	$reltype <= MusicBrainz::Server::Release::RELEASE_ATTR_SECTION_TYPE_END or $reltype = 0;

	# handle release status argument
	MusicBrainz::Server::Validation::IsNonNegInteger($relstatus) or $relstatus = 0;
	$relstatus >= MusicBrainz::Server::Release::RELEASE_ATTR_SECTION_STATUS_START or $relstatus = 0;
	$relstatus <= MusicBrainz::Server::Release::RELEASE_ATTR_SECTION_STATUS_END or $relstatus = 0;

	# handle artists argument
	$artists = ""
		unless $artists eq "single" or $artists eq "all";

	# handle offset argument
	my $max_items = 50;
	$offset = ($page-1) * $max_items if (not defined $offset and defined $page);
	$offset = 0 if not defined $offset;
	$offset = 0 if $offset < 0;

	# browse UI, wrapped in a box.
	$m->comp("/comp/tablebegin", title => "Browse Releases");
	$m->comp("/comp/browsebox",
		index => $index,
		page => "/browsevarious.html",
		reltype => $reltype,
		relstatus => $relstatus,
		artists => $artists
	);
	$m->comp("/comp/tableend");

	# filters UI, wrapped in a box.
	$m->comp("/comp/tablebegin", title => "Filter Criteria");
	$m->comp("/comp/browse/filtersbox",
		index => $index,
		reltype => $reltype,
		relstatus => $relstatus,
		artists => $artists
	);
	$m->comp("/comp/tableend");

	return if ($index eq "");

	# load album list from the database.
	my $mb = $m->comp("/comp/dblogin");
	my $al = MusicBrainz::Server::Release->new($mb->{DBH});
	$al->SetId(&ModDefs::VARTIST_ID);
	my ($num_albums, $albums) = $al->GetVariousDisplayList(
									$index,
									$offset,
									$max_items,
									$reltype,
									$relstatus, $artists
								);

	# render results-box
	my $boxtitle = sprintf "Found %d %s%s Releases starting with \"%s\"",
		$num_albums,
		($reltype || $relstatus) ? "matching " : " ",
		($artists eq "all"
			? " "
			: ($artists eq "single" ? "Single artist " : "Various artist ")
		),
		$index;

	$m->comp("/comp/tablebegin",
		title => $boxtitle,
		showmodnotice => 1,
		id => "results"
	);

	# if no artists are found, suggest entering a new one.
	if ($num_albums == 0)
	{
		$index =~ s/_/?/g;
		$index =~ s/ /_/g;

		$m->out(qq!<div style="margin-bottom: 10px">
			If you have not found the release you were are looking for,
			maybe your filter criteria are too strict. If you are sure
			the release does not exist yet, please add it to the database.</div>!);
		$m->out(qq![ <a href="/edit/album/add.html">Add New Album</a> ]!);
	}
	else
	{
		$m->comp("/comp/browse/pageselector",
			numitems => $num_albums,
			numlinks => 6,
			snaptoend => 2,
			offset => $offset,
			pagesize => $max_items,
			url => "/browsevarious.html",
			args => {
				index => $index, reltype => $reltype, relstatus => $relstatus, artists => $artists,
			},
		);

		$m->out(qq!<table class="listing" style="margin: 10px 0px">!);

		# render results in two columns
		my $rows = $max_items / 2;
		for (my $i = 0; $i < $rows; $i++)
		{
			last if (not defined $albums->[$i]); # exit condition

			# add header, if this is the first iteration
			if ($i == 0)
			{
				$m->out(qq!<tr class="header">!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!</tr>!);
			}

			my $row = 0;
			$m->out(qq!<tr>!);
			for ($albums->[$i], $albums->[$i + $rows])
			{
				if (not defined $_)
				{
					$m->out(qq!<td colspan="2">&nbsp;</td>!);
					last;
				}
				else
				{
					my $albummeta = $m->scomp(".DescribeAlbumMeta", $_);
					$albummeta =~ s/\n//;

					$m->out(sprintf qq!<td class="browsenr">%d.</td>!, $i + 1 + ($row++*$rows) + $offset);
					$m->out(sprintf qq!<td class="browsename%s">!, ($_->GetModPending ? " modpending" : ""));
					$m->out(sprintf qq!<a href="/show/release/?releaseid=%d" title="%s">%s</a></td>!,
						$_->GetId,
						$albummeta,
						encode_entities($_->GetName));
				}
			}
			$m->out(qq!</tr>!);
		}
		$m->out(qq!</table>!);

		$m->comp("/comp/browse/pageselector",
			numitems => $num_albums,
			numlinks => 6,
			snaptoend => 2,
			offset => $offset,
			pagesize => $max_items,
			url => "/browsevarious.html",
			args => {
				index => $index, reltype => $reltype, relstatus => $relstatus, artists => $artists,
			},
		);
	}
	$m->comp("/comp/tableend");
	$mb->Logout;

</%perl>
<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

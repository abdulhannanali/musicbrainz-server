%# vi: set ts=4 sw=4 ft=mason :
<%args>
	$reltype => undef
	$relstatus => undef
	$artists => ""

	$index => ""
	$offset => undef
	$page => undef
</%args>



<%def .DescribeAlbumMeta>
<%perl>
	my $al = shift;
	my $msg = sprintf "Artist: %s - %d track%s - %d discid%s - %d PUID%s - %d TRM%s",
		$al->{artistname},
		$al->GetTrackCount, ($al->GetTrackCount == 1 ? "" : "s"),
		$al->GetDiscidCount, ($al->GetDiscidCount == 1 ? "" : "s"),
		$al->GetPuidCount, ($al->GetPuidCount == 1 ? "" : "s"),
		$al->GetTrmidCount, ($al->GetTrmidCount == 1 ? "" : "s");
	$m->out($msg);
</%perl>
</%def>



<%perl>
	$m->comp("/comp/sidebar", title => 'Browse Various Artists Releases');

	# always use the uppercase index
	$index = uc($index);

	# handle release type argument
	MusicBrainz::IsNonNegInteger($reltype) or $reltype = 0;
	$reltype >= Album::ALBUM_ATTR_SECTION_TYPE_START or $reltype = 0;
	$reltype <= Album::ALBUM_ATTR_SECTION_TYPE_END or $reltype = 0;

	# handle release status argument
	MusicBrainz::IsNonNegInteger($relstatus) or $relstatus = 0;
	$relstatus >= Album::ALBUM_ATTR_SECTION_STATUS_START or $relstatus = 0;
	$relstatus <= Album::ALBUM_ATTR_SECTION_STATUS_END or $relstatus = 0;

	# handle artists argument
	$artists = ""
		unless $artists eq "single" or $artists eq "all";

	# handle offset argument
	my $max_items = 50;
	$offset = ($page-1) * $max_items if (not defined $offset and defined $page);
	$offset = 0 if not defined $offset;
	$offset = 0 if $offset < 0;

	# browse UI, wrapped in a box.
	$m->comp("/comp/tablebegin", title => "Browse Releases");
	$m->comp("/comp/browsebox",
		index => $index,
		page => "/browsevarious.html",
		reltype => $reltype,
		relstatus => $relstatus,
		artists => $artists
	);
	$m->comp("/comp/tableend");

	# filters UI, wrapped in a box.
	$m->comp("/comp/tablebegin", title => "Filter Criteria");
	$m->comp("/comp/browse/filtersbox",
		index => $index,
		reltype => $reltype,
		relstatus => $relstatus,
		artists => $artists
	);
	$m->comp("/comp/tableend");

	return if ($index eq "");

	# load album list from the database.
	my $mb = $m->comp("/comp/dblogin");
	my $al = Album->new($mb->{DBH});
	$al->SetId(&ModDefs::VARTIST_ID);
	my ($num_albums, $albums) = $al->GetVariousDisplayList(
									$index,
									$offset,
									$max_items,
									$reltype,
									$relstatus, $artists
								);

	# render results-box
	my $boxtitle = sprintf "Found %d %s%s Releases starting with \"%s\"",
		$num_albums,
		($reltype || $relstatus) ? "matching " : " ",
		($artists eq "all"
			? " "
			: ($artists eq "single" ? "Single artist " : "Various artist ")
		),
		$index;

	$m->out(qq!<a name="results"></a> !);
	$m->comp("/comp/tablebegin",
		title => $boxtitle, showmodnotice => 1
	);

	# if no artists are found, suggest entering a new one.
	if ($num_albums == 0)
	{
		$index =~ s/_/?/g;
		$index =~ s/ /_/g;

		$m->out(qq!<div style="margin-bottom: 10px">
			If you have not found the release you were are looking for,
			maybe your filter criteria are too strict. If you are sure
			the release does not exist yet, please add it to the database.</div>!);
		$m->out(qq![ <a href="/edit/release/add.html">Add New Release</a> ]!);
	}
	else
	{
		$m->comp("/comp/browse/pageselector",
			numitems => $num_albums,
			numlinks => 6,
			snaptoend => 2,
			offset => $offset,
			pagesize => $max_items,
			url => "/browsevarious.html",
			args => {
				index => $index, reltype => $reltype, relstatus => $relstatus, artists => $artists,
			},
		);

		$m->out(qq!<table class="listing" style="margin: 10px 0px">!);

		# render results in two columns
		my $rows = $max_items / 2;
		for (my $i = 0; $i < $rows; $i++)
		{
			last if (not defined $albums->[$i]); # exit condition

			# add header, if this is the first iteration
			if ($i == 0)
			{
				$m->out(qq!<tr class="header">!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!<td class="left">#:</td>!);
				$m->out(qq!<td class="left">Name:</td>!);
				$m->out(qq!</tr>!);
			}

			my $row = 0;
			$m->out(qq!<tr>!);
			for ($albums->[$i], $albums->[$i + $rows])
			{
				if (not defined $_)
				{
					$m->out(qq!<td colspan="2">&nbsp;</td>!);
					last;
				}
				else
				{
					my $albummeta = $m->scomp(".DescribeAlbumMeta", $_);
					$albummeta =~ s/\n//;

					$m->out(sprintf qq!<td class="browsenr">%d.</td>!, $i + 1 + ($row++*$rows) + $offset);
					$m->out(sprintf qq!<td class="browsename%s">!, ($_->GetModPending ? " modpending" : ""));
					$m->out(sprintf qq!<a href="/showalbum.html?albumid=%d" title="%s">%s</a></td>!,
						$_->GetId,
						$albummeta,
						encode_entities($_->GetName));
				}
			}
			$m->out(qq!</tr>!);
		}
		$m->out(qq!</table>!);

		$m->comp("/comp/browse/pageselector",
			numitems => $num_albums,
			numlinks => 6,
			snaptoend => 2,
			offset => $offset,
			pagesize => $max_items,
			url => "/browsevarious.html",
			args => {
				index => $index, reltype => $reltype, relstatus => $relstatus, artists => $artists,
			},
		);
	}
	$m->comp("/comp/tableend");
	$mb->Logout;

</%perl>
<& /comp/footer &>

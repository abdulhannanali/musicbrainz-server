%# vi: set ts=4 sw=4 ft=mason :
<%args>
$album => ""
$tindex => 0
$modid => 0
$trackname => ""
$search => ""
$notetext => ""
</%args>
<%perl>

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

MusicBrainz::IsNonNegInteger($album) && $album
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($tindex)
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($modid)
	or return $m->comp("/comp/badargs", 1, 1);

my $mb = $m->comp("/comp/dblogin");

my $al = Album->new($mb->{DBH});
$al->SetId($album);
$al->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Album #$album not found in the database.",
		1, 1,
	);

my $ar = Artist->new($mb->{DBH});
$ar->SetId($al->GetArtist);
$ar->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Artist not found in the database.",
		1, 1,
	);

my @tracks = $al->LoadTracks();

# Check if we've done all the tracks on this album

if ($tindex >= scalar(@tracks))
{
	</%perl>
	<& /comp/sidebar, title => 'Album Conversion Complete' &>
	<p>
		The moderations for changing album
		<a href="/showalbum.html?albumid=<% $album %>"
			><% $al->GetName %></a>
		by
		<a href="/showartist.html?artistid=<% $ar->GetId %>"
			><% $ar->GetName %></a>
		to a multiple artist album have been entered.&nbsp;
		Thanks!
	</p>
	<& /comp/footer &>
	<%perl>
	return;
}

my $track = $tracks[$tindex];

my $tar = Artist->new($mb->{DBH});
$tar->SetId($track->GetArtist);
$tar->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Artist not found in the database.",
		1, 1,
	);

# If the user had to click "swap", flip our "split order" flag
$session{'changetrack_splitorder'} = not $session{'changetrack_splitorder'}
	if $ARGS{'swapped'};

# Enter the mod and move on to the artist filter if the form is submitted OK
if ($search =~ /\S/ and $trackname =~ /\S/)
{
	use HTML::Mason::Tools qw( url_escape );
	my $url = "/changetrackartist.html?album=$album&tindex=$tindex&modid=$modid";
	$url .= "&search=" . url_escape($search);
	$url .= "&solo=1" if $ARGS{solo};
	$url .= "&firsttime=1"
		unless $trackname eq $track->GetName;
	
	return $m->comp("/comp/redirect", $url)
		if $trackname eq $track->GetName;

	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_EDIT_TRACKNAME,
				# --
				track => $track,
				newname => $trackname,
				# TODO depmod on $modid ?
			);

			$mods[0]->InsertNote($session{'uid'}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;
		},
		success_url => $url,
	) or return;

	die "Shouldn't get here";
}

# Scan through the track names and artists, and work out what the likely
# "split" sequence is, and what the most common track artist is.
my %trackartists;
my %split = map { $_ => 0 } (" - ", "-", " / ", "/", ": ", ":");

for my $t (@tracks)
{
	for my $split (keys %split)
	{
		++$split{$split} if index($t->GetName, $split) >= 0;
		++$trackartists{$t->GetArtist};
	}
}

my $mostcommonkey = sub {
	my $t = shift;
	my @k = sort { $t->{$a} <=> $t->{$b} } keys %$t;
	$k[-1];
};

my $split = $mostcommonkey->(\%split);
my $oldartist = $mostcommonkey->(\%trackartists);

# These are the existing values
my ($oldtrackname, $oldartistname) = ($track->GetName, $tar->GetName);

# Do a split on the track name
my ($splittrackname, $splitartistname) = split($split, $oldtrackname, 2);
($splittrackname, $splitartistname) = split(/\s+[\/-]\s+/, $oldtrackname, 2)
	unless defined $splitartistname;
my $did_split = defined $splitartistname;
($splittrackname, $splitartistname) = ($splitartistname, $splittrackname)
	if $session{'changetrack_splitorder'};

# Trim everything
for ($oldtrackname, $oldartistname, $splittrackname, $splitartistname)
{
	s/\A\s+//;
	s/\s+\z//;
	s/\s\s+/ /g;		#/# for vim
}

# Now work out what we think is the more likely - use the split values,
# or use the current values.
my ($newtrackname, $newartistname) = ($oldtrackname, $oldartistname);

if ($did_split
	and (
		$track->GetName eq $ar->GetName
		or $track->GetName eq $tar->GetName
		or $track->GetArtist == $al->GetArtist
		or $track->GetArtist == $oldartist
	))
{
	$newtrackname = $splittrackname;
	$newartistname = $splitartistname;
}

</%perl>

<& /comp/sidebar, title => 'Change Track Name and Artist' &>

<& /comp/stylemenu &>

<h2>Change Name and Artist for track '<% $track->GetName %>'</h2>

% unless ($ARGS{solo}) {
<p>
	Track <% $tindex+1 %> of <% scalar @tracks %>
</p>
% }
   
%  if ($track->GetModPending) {
<& /comp/modpending &>
%  } 
  
<p>
	Enter the new name of the track, and search for the
	artist to
	whom you'd like to move track
	'<% $track->GetName %>':
</p>

<script type="text/javascript" src="scripts/guess.js"></script>

<form method="post" action="/changetrack.html"
	name="ChangeTrackForm"
	>
	
	<table>
   		<tr>
			<td>
		 		Current Track Name:
		  	</td>
		   	<td>
				<% $oldtrackname %>
			</td>
		</tr>
		<tr>
			<td>
				Current Artist Name:
			</td>
			<td>
				<% $oldartistname %>
% if ($tar->GetSortName ne $oldartistname) {
				(<% $tar->GetSortName %>)
% }
			</td>
		</tr>
		<tr><td>&nbsp;</td></tr>
		<tr>
			<td>
				New Track Name:
			</td>
			<td>
				<input type="text" name="trackname" size="40" value="<% $newtrackname %>">
				<script type="text/javascript"><!--
				document.write(
					"<input type='button' value='Guess Case' " +
					"onclick='this.form.trackname.value = GuessCase(this.form.trackname.value)'>"
				);
				document.write(" &nbsp; ");
				//--></script>
			</td>
		</tr>
		<tr>
			<td></td>
			<td>
				<script type="text/javascript">

function SwapFields() 
{
	var f = document.forms.ChangeTrackForm;
	var tmp = f.trackname.value;
	f.trackname.value = f.search.value;
	f.search.value = tmp;
	f.swapped.value = 1 - f.swapped.value;
}

function UseCurrent() 
{
	var f = document.forms.ChangeTrackForm;
	f.search.value = f.orig_artname.value;
	f.trackname.value= f.orig_track.value;
}

% if ($did_split) {
function UseSplit() 
{
	var f = document.forms.ChangeTrackForm;
	f.search.value = f.split_artname.value;
	f.trackname.value= f.split_track.value;
}
% }

	document.writeln('<input type="button" value="Swap" onclick="return SwapFields(this)">');
	document.writeln(' ');
	document.writeln('<input type="button" value="Use Current" onclick="return UseCurrent(this)">');
% if ($did_split) {
	document.writeln(' ');
	document.writeln('<input type="button" value="Use Split" onclick="return UseSplit(this)">');
% }
				</script>
				<noscript>
				<input type="reset" value="Use Guessed">
				</noscript>
			</td>
		</tr>
		<tr>
			<td>
				Search for New Artist:
			</td>
			<td>
				<input type="text" name="search" size="40" value="<% $newartistname %>"> 
			</td>
		</tr>
		<tr>
			<td colspan="2">
				<p style="margin: 1em 0">
					Enter note: (optional):
					<br>
					<textarea name="notetext" rows="3" cols="60" style="width: 100%"></textarea>
				</p>
			</td>
		</tr>
		<tr>
			<td colspan="2" align="center">
				<input type="hidden" name="swapped" value="0">    
				<input type="submit" value="Submit">    
			</td>
		</tr>
	</table>
	
	<input type="hidden" name="album" value="<% $album %>">
	<input type="hidden" name="tindex" value="<% $tindex %>">
	<input type="hidden" name="modid" value="<% $modid %>">
	<input type="hidden" name="solo" value="<% $ARGS{solo} ? 1 : 0 %>">
	<input type="hidden" name="orig_artname" value="<% $tar->GetName %>">
	<input type="hidden" name="orig_track" value="<% $track->GetName %>">
% if ($did_split) {
	<input type="hidden" name="split_artname" value="<% $splitartistname %>">
	<input type="hidden" name="split_track" value="<% $splittrackname %>">
% }

</form>
         
<& /comp/movefocus, "ChangeTrackForm", "trackname" &>
<& /comp/footer &>

%# vi: set ts=2 sw=2 ft=mason :
<%args>
	$mbid => ""
	$albumid => ""
</%args>
<%perl>
# needs to be logged in for this
$m->comp("/comp/checkloggedin", 1, 1)
	or return;

# check if a valid mbid/albumid was given.
if ($mbid ne "") {
	MusicBrainz::IsGUID($mbid)
		or return $m->comp("/comp/badargs", 1, 1);
} else {
	MusicBrainz::IsNonNegInteger($albumid) && $albumid
		or return $m->comp("/comp/badargs", 1, 1);
}

# load album from database
my $mb = $m->comp("/comp/dblogin");
my $album = Album->new($mb->{DBH});
if ($mbid ne "") {
	$album->SetMBId($mbid);
} else {
	$album->SetId($albumid);
}
$album->LoadFromId(1)
	or return $m->comp(
		"/comp/error",
		"Album #".$album->GetId." not found in the database.",
		1, 1,
	);

# disallow editing the NAT-album
$album->IsNonAlbumTracks
	and return $m->comp(
		"/comp/error",
		"The 'Edit Album' feature cannot edit '".&Album::NONALBUMTRACKS_NAME."'",
		1, 1,
	);

# load album artist from database
my $artist = Artist->new($mb->{DBH});
$artist->SetId($album->GetArtist);
$artist->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Artist does not exist",
		1, 1,
	);

# load tracks, and add into @t list.
my @t = $album->LoadTracks;
@t = map {
	+{
		id					=> $_->GetId,
		name				=> $_->GetName,
		artist			=> $_->GetArtist,
		artist_name	=> $_->GetArtistName,
		seq					=> $_->GetSequence,
		length			=> $_->GetLength,
		orig_name		=> $_->GetName,
		orig_seq		=> $_->GetSequence,
		orig_length	=> $_->GetLength,
	},
} @t;

# load releasetype, status
my ($type, $status) = $album->GetReleaseTypeAndStatus;

# compile information into session variable.
my %edit = (
	id					=> $album->GetId,
	name				=> $album->GetName,
	type				=> $type,
	status			=> $status,
	orig_name		=> $album->GetName,
	orig_type		=> $type,
	orig_status	=> $status,
	artist			=> {
		id				=> $artist->GetId,
		name			=> $artist->GetName,
		sortname	=> $artist->GetName,
	},
	tracks			=> \@t,
	can_add_remove 	=> ($album->CanAddTrack or $album->CanRemoveTrack),
	is_various		=> ($album->HasMultipleTrackArtists),
);

$session{editalbum} = \%edit;

# forward to form
return $m->comp(
	"/comp/redirect",
	"/edit/album/sae-edittracks.html?albumid=" . $album->GetId
);

</%perl>

<%args>

	@id => ()
	$untag => 0

	$attr_type => 0
	$attr_status => 0

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# there has to be at least one release
	(@id > 0) or return $m->comp("/comp/badargs", 1, 1);

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	# load 1..n releases from the database, and track
	# release artistids in artisthash
	my @releaseobjs;
	my %artisthash;
	for my $releaseid (@id)
	{
		MusicBrainz::IsNonNegInteger($releaseid) && $releaseid
			or return $m->comp("/comp/badargs", 1, 1);

		my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);

		# skip [non-album tracks] release.
		if (!$release->IsNonAlbumTracks)
		{
			push @releaseobjs, $release;
			++$artisthash{$release->GetArtist};
		}
	}

	# if no editable releases were provided
	if (not @releaseobjs)
	{
		return $m->comp("/comp/layout/badarguments", text => "No editable releases provided.");
	}

	# we got a form submission
	if ($submitvalue ne "")
	{
		if ($submitvalue =~ /Cancel/i)
		{
			# prepare url for cancel redirct:
			# * only one release, redirect to the release
			# * multiple releases, redirect to the batchop page
			my $url =
				@id == 1
					? "/release/" . $releaseobjs[0]->GetMBId . ".html"
					: "/edit/albumbatch/done.html";

			$m->comp("/comp/redirect", $url);
		}
		elsif ($submitvalue =~ /Enter Edits/i)
		{
			# prepare url for enter mods redirct:
			# * only one release, redirect to the release
			# * to the artist of release 0, if it was found
			# * if neither case is satisfied, redirect to /search.html
			my $url =
				@id == 1
					? "/release/" . $releaseobjs[0]->GetMBId . ".html"
					: keys(%artisthash) == 1
						? "/show/artist/?artistid=".$releaseobjs[0]->GetArtist
						: "/search.html";

			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {
					my @mods = Moderation->InsertModeration(
						DBH	=> $mb->{DBH},
						uid	=> $session{uid},
						privs => $session{privs},
						type => &ModDefs::MOD_EDIT_ALBUMATTRS,
						# --
						albums => \@releaseobjs,
						attr_type => $attr_type,
						attr_status => $attr_status,
					);

					$mods[0]->InsertNote($session{'uid'}, $notetext)
						if $mods[0]
						and $notetext =~ /\S/;

					delete $session{'tagged_albums'}
						if $untag;
				},
				success_url => $url,
			) or return;

			die "Shouldn't get here";
		}
	}

	# get a hash of release attributes, and set the current attributes
	# to the ones with the highest frequency.
	my %attrs;
	for my $release (@releaseobjs)
	{
		++$attrs{$_}
			for $release->GetAttributes;
	}
	my @attrs = sort { $a<=>$b } keys %attrs;
	my ($type, $status) = Album->GetReleaseTypeAndStatus(\@attrs);


	my @focus_to;

	# see if one of the releases has a moderation pending flag.
	my $mp = grep { $_->GetLanguageModPending } @releaseobjs;

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit Album Attributes" &>

	<!-- add editsuite for modnote resizer support -->
	<script type="text/javascript" src="/scripts/editsuite.js"></script>

	<form method="post" action="/edit/album/editattributes.html" id="EditAlbumAttrsForm">

%	if (@releaseobjs == 1)
%	{

		<& /comp/layout/editformbegin, title => "Edit Release Attributes",
			release => $releaseobjs[0],
			mp_type => &ModDefs::MOD_EDIT_ALBUMATTRS,
		&>

%	}
%	else
%	{

		<& /comp/layout/editformbegin, title => "Edit Release Attributes",
			mp_type => &ModDefs::MOD_EDIT_ALBUMATTRS,
			mp => $mp,
		&>
%	}

		<& /edit/album/attrform,
			adding				=> undef,
			releaseobjs			=> \@releaseobjs,
			#albumname			=> (@releaseobjs > 1 ? "" : $releaseobjs[0]->GetName),
			#multi_album			=> (@releaseobjs > 1),

			type_value			=> $type,
			status_value		=> $status,
			type_fieldname		=> "attr_type",
			status_fieldname	=> "attr_status",
			focus_to			=> \@focus_to,
		&>

% 	for (@id)
%	{

		<input type="hidden" name="id" value="<% $_ %>">

% 	}

		<input type="hidden" name="untag" value="<% $untag %>">
		<& /comp/layout/editformend &>

	</form>

	<& /comp/movefocus, "EditAlbumAttrsForm", $focus_to[0] &>
<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to edit the language/script of a release
	#
	# $Id$
	#
</%perl>
<%args>

	@id => ()

	$language_new => undef
	$script_new => undef

	$expand_lists => 0
	$confirm => 0
	$untag => 0

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# there has to be at least one release
	(@id > 0) or return $m->comp("/comp/badargs", 1, 1);

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	# load 1..n releases from the database, and track
	# release artistids in artisthash
	my @releaseobjs = ();
	my %artisthash;
	foreach my $releaseid (@id)
	{
		MusicBrainz::Server::Validation::IsNonNegInteger($releaseid) && $releaseid
			or return $m->comp("/comp/badargs", 1, 1);

		my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);
		push @releaseobjs, $release;

		++$artisthash{$release->GetArtist};
	}

	# convert "i don't know" to 0.
	$script_new = 0 if (defined $script_new and $script_new eq "");
	$language_new = 0 if (defined $language_new and $language_new eq "");

	my @errors = ();

	# we got a a form submission
	if ($submitvalue ne "")
	{
		push @errors, "Please select the language of the selected releases." if (!MusicBrainz::Server::Validation::IsNonNegInteger($language_new));
		push @errors, "Please select the script of the selected releases." if (!MusicBrainz::Server::Validation::IsNonNegInteger($script_new));

		if ($submitvalue =~ /Show (reduced|full) lists/i)
		{
			$expand_lists = !$expand_lists;
			@errors = ();
		}
		elsif ($submitvalue =~ /Guess/i)
		{
			$script_new = undef;
			$language_new = undef;

			# clear errors after guessing, they'll be reshown again after
			# the next submit, if there are still any.
			@errors = ();
		}
		elsif ($submitvalue =~ /Cancel/i)
		{
			# prepare url for cancel redirct:
			# * only one release, redirect to the release
			# * multiple releases, redirect to the batchop page
			my $url =
				@id == 1
					? "/release/" . $releaseobjs[0]->GetMBId . ".html"
					: "/edit/albumbatch/done.html";

			$m->comp("/comp/redirect", $url);
		}
		elsif (@errors == 0)
		{
			# prepare url for enter mods redirct:
			# * only one release, redirect to the release
			# * to the artist of release 0, if it was found
			# * if neither case is satisfied, redirect to /search.html
			my $url =
				@id == 1
					? "/release/" . $releaseobjs[0]->GetMBId . ".html"
					: keys(%artisthash) == 1
						? "/show/artist/?artistid=" . $releaseobjs[0]->GetArtist
						: "/search.html";

			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {
					my @mods = Moderation->InsertModeration(
						DBH	=> $mb->{DBH},
						uid	=> $session{uid},
						privs => $session{privs},
						type => &ModDefs::MOD_EDIT_RELEASE_LANGUAGE,
						# --
						albums => \@releaseobjs,
						language => $language_new,
						script => $script_new,
					);

					$mods[0]->InsertNote($session{"uid"}, $notetext)
						if $mods[0]
						and $notetext =~ /\S/;

					delete $session{"tagged_albums"}
						if $untag;
				},
				success_url => $url,
			) or return;

			die "Shouldn't get here";
		}
	}
	else
	{
		# if we are editing one release, load the current values from
		# the database, else guess them from the list of releases.
		if (@releaseobjs == 1)
		{
			my $release = $releaseobjs[0];
			$script_new = $release->GetScriptId;
			$language_new = $release->GetLanguageId;
		}
	}

	# let's see if we have to guess language or script from
	# the title fields of this (or the first) release.
	# --
	# TODO: would it possibly make more sense to take all fields
	# of all releases into account?
	if (not defined $script_new
		or not defined $language_new)
	{
		my @text;

		if (@releaseobjs)
		{
			my $release = $releaseobjs[0];
			push @text, $release->GetName();
			push @text, $_->GetName() foreach ($release->LoadTracks());
		}

		# if the script is not set, guess it from the text.
		if (not defined $script_new)
		{
			$script_new = MusicBrainz::Server::Script->GuessFromText($mb->{DBH}, \@text);
			$script_new = ($script_new ? $script_new->GetId : -1);
		}

		# if the language is not set, guess it from the text and $script
		if (not defined $language_new)
		{
			$language_new = MusicBrainz::Server::Language->GuessFromTextAndScript(
								$mb->{DBH}, \@text, $script_new
							);
			$language_new = ($language_new ? $language_new->GetId : -1);
		}
	}

	# set the current values from the argument, or the first release handed in.
	my $language_curr = $language_new || (@releaseobjs ? $releaseobjs[0]->GetLanguageId : -1) || -1;
	my $script_curr = $script_new || (@releaseobjs ? $releaseobjs[0]->GetScriptId : -1) || -1;

	# see if one of the releases has a modpending flag.
	my $mp = grep { $_->GetLanguageModPending } @releaseobjs;

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit release language/script" &>

	<!-- add editsuite for modnote resizer support -->
	<script type="text/javascript" src="/scripts/editsuite.js"></script>

	<form method="post" action="/edit/albumlanguage/edit.html" id="EditAlbumLanguageForm">

%	if (@releaseobjs == 1)
%	{

		<& /comp/layout/editformbegin, title => "Edit release language/script",
			release => $releaseobjs[0],
			mp_type => &ModDefs::MOD_EDIT_RELEASE_LANGUAGE,
		&>

%	}
%	else
%	{

		<& /comp/layout/editformbegin, title => "Edit release language/script",
			mp_type => &ModDefs::MOD_EDIT_RELEASE_LANGUAGE,
			mp => $mp,
		&>

%	}

		<table class="formstyle">
			<tr>
				<td class="instructions" colspan="2">
					<ul>
						<li>
							Please choose the <& /comp/linkdoc, "ReleaseLanguage", "language/script" &>
							of the selected releases.</li>
					</ul>
				</td>
			</tr>

%	if (@errors)
%	{

			<tr>
				<td>&nbsp;</td>
				<td>
					<& /comp/form/feedbackbox,
						"warning", "Your form submission contains errors:",
						"<ul><li>"
					  . join ("</li><li>", @errors)
					  . "</li></ul>" &></td>
			</tr>

%	}

			<tr class="top">
				<td class="label">
					<label>Selected releases:</label></td>
				<td>
				<table class="releaselist">
					<tr class="header">
						<td>Release title:</td>
						<td>Language:</td>
						<td>Script:</td>
					</tr>

<%perl>

	my $n = 0;
	foreach my $release ( @releaseobjs )
	{
		$m->out("<tr>");

		# TODO: load artist for this?
		# $m->out(qq!<td style="padding-right: 40px">!);
		# $m->comp("/comp/linkartist", artist => $release->GetArtist);
		# $m->out(qq!</td><td style="padding-right: 40px">!);

		$m->out(qq!<td style="padding-right: 40px">!);
		$m->comp("/comp/form/currentvalue", release => $release, mp_type => &ModDefs::MOD_EDIT_RELEASE_LANGUAGE);
		$m->out(qq!</td><td style="padding-right: 40px">!);
		$m->out(
			defined $release->GetLanguage
				? $release->GetLanguage->GetName
				: "Not Set");
		$m->out(qq!</td><td style="padding-right: 40px">!);
		$m->out(
			defined $release->GetScript
				? $release->GetScript->GetName
				: "Not Set");
		$m->out("</td></tr>");
	}

</%perl>

					</table>
				</td>
			</tr>

			<& /edit/albumlanguage/form,
				language_curr => $language_curr,
				script_curr	=> $script_curr,
				language_fieldname => "language_new",
				script_fieldname => "script_new",
				expand_lists => $expand_lists,
				notetext => $notetext,
			&>
		</table>

% 	foreach my $releaseid (@id)
%	{
%		$m->comp("/comp/form/hiddenfield", "id", $releaseid);
% 	}

		<& /comp/form/hiddenfield, "untag", $untag &>
		<& /comp/form/hiddenfield, "expand_lists", $expand_lists &>
		<& /comp/form/focusfield, "language_new" &>

		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

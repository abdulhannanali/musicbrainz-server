%# vi: set ts=2 sw=2 ft=mason :
<%args>
@id => ()
$untag => 0
$confirm => 0
$album => undef
$newlanguage => undef
$newscript => undef
$notetext => ""
$show_full_lists => 0
$button_toggle_lists => undef
$button_submit => undef
</%args>
<%perl>

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

foreach my $album ( @id )
{
	MusicBrainz::IsNonNegInteger($album) && $album
		or return $m->comp("/comp/badargs", 1, 1);
}

my $mb = $m->comp("/comp/dblogin");

my @objs;
my %artists;

foreach my $albumid ( @id )
{
	my $al = Album->new($mb->{DBH});
	$al->SetId($albumid);
	$al->LoadFromId
		or return $m->comp(
			"/comp/error",
			"Album #$albumid does not exist",
			1, 1,
		);

	push @objs, $al;
	++$artists{$al->GetArtist};
}

my $url = 
	@id == 1
		? "/showalbum.html?albumid=" . $id[0]
		: keys(%artists) == 1
			? "/showartist.html?artistid=" . $objs[0]->GetArtist
			: "/search.html";

$newscript = 0 if defined $newscript and $newscript eq '';
$newlanguage = 0 if defined $newlanguage and $newlanguage eq '';

my @text;
if ( @objs )
{
	my $al = $objs[0];
	push @text, $al->GetName();
	push @text, $_->GetName() foreach $al->LoadTracks();
}

if ( not defined $newscript )
{
	$newscript = MusicBrainz::Server::Script->GuessFromText($mb->{DBH}, \@text);
	$newscript = ($newscript ? $newscript->GetId : 0);
}

if ( not defined $newlanguage )
{
	$newlanguage = MusicBrainz::Server::Language->GuessFromTextAndScript(
												$mb->{DBH}, \@text, $newscript);
	$newlanguage = ($newlanguage ? $newlanguage->GetId : 0);
}

if ($button_submit and not $button_toggle_lists
	and MusicBrainz::IsNonNegInteger($newlanguage)
	and MusicBrainz::IsNonNegInteger($newscript)
) {
	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_EDIT_ALBUM_LANGUAGE,
				# --
				albums => \@objs,
				language => $newlanguage,
				script => $newscript,
			);

			$mods[0]->InsertNote($session{'uid'}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;

			delete $session{'tagged_albums'}
				if $untag;
		},
		success_url => $url,
	) or return;

	die "Shouldn't get here";
}

my $old_language_id = $newlanguage || $objs[0]->GetLanguageId || '';
my $old_script_id = $newscript || $objs[0]->GetScriptId || '';

$show_full_lists = ! $show_full_lists if $button_toggle_lists; 

</%perl>

<& /comp/sidebar, title => 'Edit Album Language' &>

<& /comp/stylemenu &>

<form method="post" action="/edit/albumlanguage/edit.html"
	name="EditAlbumLanguageForm"
	enctype="application/x-www-form-urlencoded">

	<fieldset>
		<legend>Selected Albums</legend>
		<div class="padleft">
			<table>
				<tr>
					<td><b>Album Name:</td>
					<td rowspan="100">&nbsp;&nbsp;</td> 
					<td><b>Language:</td>
					<td rowspan="100">&nbsp;&nbsp;</td>
					<td><b>Script:</td>
				</tr>
% my $n = 0;
% foreach my $al ( @objs ) {
				<tr <% ($al->GetLanguageModPending) ? 'class="mp"' : '' |n %>>
					<td><a href="/showalbum.html?albumid=<% $al->GetId %>"
								><% $al->GetName %></a></td>
%	if ( defined $al->GetLanguage ) {
					<td><% $al->GetLanguage->GetName %></td>
%	} else {
					<td><em>Not set</em></td>
%	}
%	if ( defined $al->GetScript ) {
					<td><% $al->GetScript->GetName %></td>
%	} else {
					<td><em>Not set</em></td>
%	}
				</tr>
% }
			</table>
		</div>
	</fieldset>

	<fieldset>
		<legend>Edit Album Language</legend>
		<& /edit/albumlanguage/form,
			language_value		=> $old_language_id,
			script_value		=> $old_script_id,
			language_fieldname	=> 'newlanguage',
			script_fieldname	=> 'newscript',
			show_full_lists		=> $show_full_lists,
			notetext 			=> $notetext,
		&>
	</fieldset>

% foreach my $albumid ( @id ) {
	<input type="hidden" name="id" value="<% $albumid %>">
% }
	<input type="hidden" name="untag" value="<% $untag %>">
	<input type="hidden" name="show_full_lists" value="<% $show_full_lists %>">
</form>

<& /comp/movefocus, "EditAlbumLanguageForm", "newlanguage" &>
<& /comp/footer &>

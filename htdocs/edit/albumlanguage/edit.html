%# vi: set ts=2 sw=2 ft=mason :
<%args>
	@id => ()

	$language_new => undef
	$script_new => undef

	$submit => ""
	$expand_lists => 0
	$confirm => 0
	$untag => 0
	$notetext => ""
</%args>
<%perl>
	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# there has to be at least one release
	(@id > 0) or return $m->comp("/comp/badargs", 1, 1);

	# get database handle
	my $mb = $m->comp("/comp/dblogin");

	# load 1..n releases from the database, and track
	# release artistids in artisthash
	my @releaseobjs = ();
	my %artisthash;
	foreach my $releaseid (@id)
	{
		MusicBrainz::IsNonNegInteger($releaseid) && $releaseid
			or return $m->comp("/comp/badargs", 1, 1);

		my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);
		push @releaseobjs, $release;

		++$artisthash{$release->GetArtist};
	}

	# convert empty values to internal int value (0).
	$script_new = 0 if (defined $script_new and $script_new eq "");
	$language_new = 0 if (defined $language_new and $language_new eq "");

	# if $submit is not empty, we handle a form submission
	if ($submit ne "")
	{
		# prepare redirect url after cancel/enter mods event:
		# * only one release, redirect to the release
		# * to the artist of release 0, if it was found
		# * if neither case is satisfied, redirect to /search.html
		my $url =
			@id == 1
				? "/album/" . $releaseobjs[0]->GetMBId . ".html"
				: keys(%artisthash) == 1
					? "/showartist.html?artistid=" . $releaseobjs[0]->GetArtist
					: "/search.html";

		if ($submit =~ /Show (Reduced|Full) Lists/i)
		{
			$expand_lists = !$expand_lists;
		}
		elsif ($submit =~ /Guess/i)
		{
			$script_new = undef;
			$language_new = undef;
		}
		elsif ($submit =~ /Cancel/i)
		{
			$m->comp("/comp/redirect", $url);
		}
		elsif ($submit =~ /Enter Moderations/i
				and MusicBrainz::IsNonNegInteger($language_new)
				and MusicBrainz::IsNonNegInteger($script_new))
		{
			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {
					my @mods = Moderation->InsertModeration(
						DBH	=> $mb->{DBH},
						uid	=> $session{uid},
						privs => $session{privs},
						type => &ModDefs::MOD_EDIT_ALBUM_LANGUAGE,
						# --
						albums => \@releaseobjs,
						language => $language_new,
						script => $script_new,
					);

					$mods[0]->InsertNote($session{"uid"}, $notetext)
						if $mods[0]
						and $notetext =~ /\S/;

					delete $session{"tagged_albums"}
						if $untag;
				},
				success_url => $url,
			) or return;

			die "Shouldn't get here";
		}
	}

	# let's see if we have to guess language or script from
	# the title fields of this (or the first) release.
	# --
	# TODO: would it possibly make more sense to take all fields
	# of all releases into account?
	if (not defined $script_new
		or not not defined $language_new)
	{
		my @text;
		if (@releaseobjs)
		{
			my $release = $releaseobjs[0];
			push @text, $release->GetName();
			push @text, $_->GetName() foreach ($release->LoadTracks());
		}

		# if the script is not set, guess it from the text.
		if (not defined $script_new)
		{
			$script_new = MusicBrainz::Server::Script->GuessFromText($mb->{DBH}, \@text);
			$script_new = ($script_new ? $script_new->GetId : 0);
		}

		# if the language is not set, guess it from the text and $script
		if (not defined $language_new)
		{
			$language_new = MusicBrainz::Server::Language->GuessFromTextAndScript(
								$mb->{DBH}, \@text, $script_new
							);
			$language_new = ($language_new ? $language_new->GetId : 0);
		}
	}

	# set the current values from the argument, or the first release handed in.
	my $language_curr = $language_new || (@releaseobjs ? $releaseobjs[0]->GetLanguageId : "") || "";
	my $script_curr = $script_new || (@releaseobjs ? $releaseobjs[0]->GetScriptId : "") || "";

	# see if one of the releases has a moderation pending flag.
	my $mp = grep { $_->GetLanguageModPending } @releaseobjs;

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit Release Language/Script" &>

	<!-- add editsuite for modnote resizer support -->
	<script type="text/javascript" src="/scripts/editsuite.js"></script>

	<form method="post" action="/edit/albumlanguage/edit.html" id="EditAlbumLanguageForm">

%	if (@releaseobjs == 1) {
		<& /comp/layout/editformbegin, title => "Edit Release Language/Script",
			release => $releaseobjs[0],
			mp_type => &ModDefs::MOD_EDIT_ALBUM_LANGUAGE,
		&>
%	} else {
		<& /comp/layout/editformbegin, title => "Edit Release Language/Script",
			mp_type => &ModDefs::MOD_EDIT_ALBUM_LANGUAGE,
			mp => $mp,
		&>
%	}

		<table class="formstyle">
			<tr>
				<td class="instructions" colspan="2">
					<ul>
						<li>
							Please choose the <a href="/doc/AlbumLanguage">script and language</a>
							of the selected releases.</li>
					</ul>
				</td>
			</tr>
			<tr class="top">
				<td class="label">
					<label>Selected Releases:</label></td>
				<td>
				<table class="releaselist">
					<tr class="header">
						<td>Release Name</td>
						<td>Language</td>
						<td>Script</td>
					</tr>

					<%perl>
						my $n = 0;
						foreach my $release ( @releaseobjs )
						{
							$m->out("<tr>");

# TODO: load artist for this?
#							$m->out(qq!<td style="padding-right: 40px">!);
#							$m->comp("/comp/linkartist", artist => $release->GetArtist);
#							$m->out(qq!</td><td style="padding-right: 40px">!);

							$m->out(qq!<td style="padding-right: 40px">!);
							$m->comp("/comp/form/currentvalue", release => $release, mp_type => &ModDefs::MOD_EDIT_ALBUM_LANGUAGE);
							$m->out(qq!</td><td style="padding-right: 40px">!);
							$m->out(
								defined $release->GetLanguage
									? $release->GetLanguage->GetName
									: "Not Set");
							$m->out(qq!</td><td style="padding-right: 40px">!);
							$m->out(
								defined $release->GetScript
									? $release->GetScript->GetName
									: "Not Set");
							$m->out("</td></tr>");
						}
					</%perl>
					</table>
				</td>
			</tr>

			<& /edit/albumlanguage/form,
				language_curr => $language_curr,
				script_curr	=> $script_curr,
				language_fieldname => "language_new",
				script_fieldname => "script_new",
				expand_lists => $expand_lists,
				notetext => $notetext,
			&>
		</table>

% 	foreach my $releaseid (@id) {
		<input type="hidden" name="id" value="<% $releaseid %>" />
% 	}
		<input type="hidden" name="untag" value="<% $untag %>" />
		<input type="hidden" name="expand_lists" value="<% $expand_lists %>" />

		<& /comp/movefocus, "EditAlbumLanguageForm", "language_new" &>
		<& /comp/layout/editformend &>
	</form>

<& /comp/footer &>

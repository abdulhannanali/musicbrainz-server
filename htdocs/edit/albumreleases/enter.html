<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Enter the release events edit into the database, unless cancel
	# was pressed on the previous page.
	#
	# $Id$
	#
</%perl>
<%args>

	$releaseid

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# prepare redirect urls
	my $url = "/show/release/?releaseid=" . $releaseid;
	my $cancelurl = "/edit/albumreleases/cancel.html?releaseid=".$releaseid;

	# let's see if we still got a session, else redirect
	# to the given releaseid.
	my $edit = $session{"editreleases"}{$releaseid}
		or return $m->comp("/comp/redirect", $url);

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# load release from database.
	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);

	# find out what to do with each set of release events
	my $debug = 0;
	my (@adds, @edits, @removes);
	for my $rel (@{ $edit->{"releases"} })
	{
		my $revnew = MusicBrainz::Server::ReleaseEvent->new($mb->{DBH});
		$revnew->SetRelease($release->GetId);
		$revnew->SetCountry($rel->{"country"});
		$revnew->SetYMD(@$rel{qw( year month day )});
		$revnew->SetLabel($rel->{label});
		$revnew->SetCatNo($rel->{catno});
		$revnew->SetBarcode($rel->{barcode});
		$revnew->SetFormat($rel->{format});

		if ($rel->{"new"})
		{
			next unless $rel->{"country"};
			push @adds, $revnew;

			$m->out("Add Release Event: " . $revnew->ToString . "<br/>")
				if ($debug);
			next;
		}

		my $revold = MusicBrainz::Server::ReleaseEvent->new($mb->{DBH});
		$revold = $revold->newFromId($rel->{"id"})
			or next;

		if ($rel->{"delete"})
		{
			push @removes, $revold;
			$m->out("Remove Release Event: " . $revold->ToString . "<br/>")
				if ($debug);
			next;
		}

		next if 
			int($rel->{orig_year}) == int($rel->{year}) and
			int($rel->{orig_month}) == int($rel->{month}) and
			int($rel->{orig_day}) == int($rel->{day}) and
			int($rel->{orig_country}) == int($rel->{country}) and
			int($rel->{orig_label}) == int($rel->{label}) and
			$rel->{orig_catno} eq $rel->{catno} and
			$rel->{orig_barcode} eq $rel->{barcode} and
			$rel->{orig_format} eq $rel->{format};

		push @edits, {
			object => $revold,
			country => $rel->{"country"},
			year => $rel->{"year"},
			month => $rel->{"month"},
			day => $rel->{"day"},
			label => $rel->{"label"},
			catno => $rel->{"catno"},
			barcode => $rel->{"barcode"},
			format => $rel->{"format"},
		};

		$m->out("Edit Release Event: " . $revold->ToString . " -> " . $revnew->ToString . "<br/>")
			if ($debug);
	}

	exit if ($debug);

	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @insertededits;

			push @insertededits, Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_ADD_RELEASE_EVENTS,
				# --
				album => $release,
				adds => \@adds,
			) if (@adds);

			push @insertededits, Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_EDIT_RELEASE_EVENTS,
				# --
				album => $release,
				edits => \@edits,
			) if (@edits);

			push @insertededits, Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_REMOVE_RELEASE_EVENTS,
				# --
				album => $release,
				removes => \@removes,
			) if (@removes);

			for my $edit (@insertededits)
			{
				$edit->InsertNote($session{"uid"}, $notetext)
					if $notetext =~ /\S/;
			}
		},
		#success_url => $url,
	) or return;

	# remove current release from the session and show the release again.
	return $m->comp("/comp/redirect", $cancelurl);

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Enter the release events edit into the database, unless cancel
	# was pressed on the previous page.
	#
	# $URL$
	# $Id$
	#
</%perl>
<%args>
	$releaseid

	$submit => ""
	$notetext => ""
</%args>
<%perl>

	# prepare redirect urls
	my $url = "/show/release/?releaseid=" . $releaseid;
	my $cancelurl = "/edit/albumreleases/cancel.html?releaseid=".$releaseid;

	# let's see if we still got a session, else redirect
	# to the given releaseid.
	my $edit = $session{"editreleases"}{$releaseid}
		or return $m->comp("/comp/redirect", $url);

	# remove current release from the session and show the release again.
	if ($submit =~ m/Cancel/i)
	{
		return $m->comp("/comp/redirect", $cancelurl);
	}

	# load release from database.
	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);

	# find out what to do with each set of release events
	my (@adds, @edits, @removes);
	for my $rel (@{ $edit->{"releases"} })
	{
		if ($rel->{"new"})
		{
			next unless $rel->{"year"} and $rel->{"country"};
			my $obj = MusicBrainz::Server::Release->new($mb->{DBH});
			$obj->SetAlbum($release->GetId);
			$obj->SetCountry($rel->{"country"});
			$obj->SetYMD(@$rel{qw( year month day )});
			push @adds, $obj;
			next;
		}

		my $obj = MusicBrainz::Server::Release->new($mb->{DBH});
		$obj = $obj->newFromId($rel->{"id"})
			or next;

		if ($rel->{"delete"})
		{
			push @removes, $obj;
			next;
		}

		my $changed = 0;
		for (keys %$rel)
		{
			next unless /^orig_(.*)$/;
			$changed = 1, last
				if $rel->{$_} != $rel->{$1};
		}
		$changed or next;

		push @edits, {
			object => $obj,
			country => $rel->{"country"},
			year => $rel->{"year"},
			month => $rel->{"month"},
			day => $rel->{"day"},
		};
	}

	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_EDIT_RELEASES,
				# --
				album	=> $release,
				adds	=> \@adds,
				edits	=> \@edits,
				removes	=> \@removes,
			);

			$mods[0]->InsertNote($session{"uid"}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;
		},
		#success_url => $url,
	) or return;

	# remove current release from the session and show the release again.
	return $m->comp("/comp/redirect", $cancelurl);

</%perl>

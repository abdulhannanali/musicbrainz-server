<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Load the current release events of this release from the
	# database and redirect to the edit release events form.
	#
	# $Id$
	#
</%perl>
<%args>

	$album => ""
	$releaseid => ""

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# backward compability
	$releaseid ||= $album;

	# make sure we got a valid releaseid parameter
	MusicBrainz::Server::Validation::IsNonNegInteger($releaseid) && $releaseid
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>releaseid</strong> is missing or has a wrong format.");

	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);

	$release->IsNonAlbumTracks
		and return $m->comp(
			"/comp/error",
			"The 'Edit Release Events' feature cannot edit '".&Album::NONALBUMTRACKS_NAME."'",
			1, 1,
		);

	my $artist = $m->comp("/comp/loadartist", $mb, $release->GetArtist);

	# fill the release events into the "r" list.
	my @r = $release->Releases;
	@r = map {
		my @ymd = $_->GetYMD;
		my $label_name = $_->GetLabel ? $_->Label->GetName : undef;
		+{
			id => $_->GetId,
			country => $_->GetCountry,
			year => $ymd[0],
			month => $ymd[1],
			day => $ymd[2],
			sortdate => $_->GetSortDate,
			label_name => $label_name,
			catno => $_->GetCatNo,
			barcode => $_->GetBarcode,
			orig_country => $_->GetCountry,
			orig_year => $ymd[0],
			orig_month => $ymd[1],
			orig_day => $ymd[2],
			orig_label => $_->GetLabel,
			orig_label_name => $label_name,
			orig_catno => $_->GetCatNo,
			orig_barcode => $_->GetBarcode,
			modpending => $_->GetModPending,
		},
	} @r;

	# prepare session variable
	my %edit = (
		id => $release->GetId,
		name => $release->GetName,
		artist => {
			id => $artist->GetId,
			name => $artist->GetName,
			sortname => $artist->GetName,
		},
		releases => \@r,
	);

	# Store release events into session variable
	$session{editreleases}{$releaseid} = \%edit;

	# Ask for the list of countries - this causes it to be read and cached now
	delete $session{countries_menu};
	() = $m->comp("countries-menu.inc", $mb->{DBH});

	# Redirect to form.
	return $m->comp("/comp/redirect", "/edit/albumreleases/editreleases.html?releaseid=".$releaseid);

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# Edit an existing artist
	#
	# $Id$
	#
</%perl>
<%args>

	$id => ""
	$artistid => undef

	$name => ""
	$sortname => ""
	$type => -1

	$begindate_y => ""
	$begindate_m => ""
	$begindate_d => ""
	$enddate_y => ""
	$enddate_m => ""
	$enddate_d => ""

	$resolution => ""
	$confirm => 0

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# backwards compability.
	$artistid ||= $id;

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	MusicBrainz::Server::Validation::IsNonNegInteger($artistid) && $artistid
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument artistid has an invalid format.");

	$artistid != &ModDefs::VARTIST_ID and $artistid != &ModDefs::DARTIST_ID
		or return $m->comp("/comp/layout/badarguments",
			text => "Editing the DELETED_ARTIST or VARIOUS_ARTISTS is not allowed.");

	MusicBrainz::Server::Validation::IsSingleLineString($name)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument name has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($sortname)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument sortname has an invalid format.");

	MusicBrainz::Server::Validation::IsSingleLineString($resolution)
		or return $m->comp("/comp/layout/badarguments",
			text => "Argument resolution has an invalid format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);


	# Instantiate MusicBrainz object, load artist from database
	my $mb = $m->comp("/comp/dblogin");
	my $artist = $m->comp("/comp/loadartist", $mb, $artistid);

	$name = $artist->GetName() if !$name;
	$sortname = $artist->GetSortName() if !$sortname;

	MusicBrainz::Server::Validation::TrimInPlace($resolution, $name, $sortname);

	# Try to load the named artist by name. If an artist comes up,
	# give the user a stern warning about editing a duplicate artist.
	my @dupes;
	if ($name ne "")
	{
		my $artist = MusicBrainz::Server::Artist->new($mb->{DBH});
		my $artists = $artist->GetArtistsFromName($name);

		my $norm_name = MusicBrainz::Server::Validation::NormaliseSortText($name);
		@dupes = grep {
			$_->GetId != $artistid
			&&
			MusicBrainz::Server::Validation::NormaliseSortText($_->GetName) eq $norm_name
		} @$artists;
	}

	# if we have a value in submitvalue, the form was submitted
	my @messages = ();
	if ($submitvalue ne "")
	{
		my $url = "/artist/".$artist->GetMBId.".html";

		if ($submitvalue =~ m/Cancel/i)
		{
			return $m->comp("/comp/redirect", $url);
		}
		else
		{
			push @messages, "Please Select a valid type." if (not MusicBrainz::Server::Artist::IsValidType($type));
			push @messages, "Please Enter the Name." if ($name eq "");
			push @messages, "Please Enter the Sort Name." if ($sortname eq "");
			push @messages, "Begin Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d));
			push @messages, "Begin Date has to be either valid or empty"
				if (not MusicBrainz::Server::Validation::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d));
			push @messages, "Begin Date has to be earlier than End Date"
				if (not MusicBrainz::Server::Validation::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d,
													   $enddate_y, $enddate_m, $enddate_d));
			push @messages, "Please enter a comment and activate the confirm checkbox"
				if (@dupes and ($resolution eq "" or not $confirm));

			push @messages, "You have not made any changes"
				if ($name eq $artist->GetName and
					$sortname eq $artist->GetSortName and
					$resolution eq $artist->GetResolution and
					$type == $artist->GetType and
					MusicBrainz::Server::Validation::MakeDBDateStr($begindate_y, $begindate_m, $begindate_d) eq $artist->GetBeginDate and
					MusicBrainz::Server::Validation::MakeDBDateStr($enddate_y, $enddate_m, $enddate_d) eq $artist->GetEndDate);

			# no error message was raised, we can go ahead and add
			# the new artist entry.
			if (@messages == 0)
			{
				$m->comp(
					"/comp/entermods",
					DBH => $mb->{DBH},
					sub => sub {
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_ARTIST,
							# --
							artist => $artist,
							name => $name,
							sortname => $sortname,
							artist_type => $type,
							resolution => $resolution,
							begindate => [ $begindate_y, $begindate_m, $begindate_d ],
							enddate => [ $enddate_y, $enddate_m, $enddate_d ],
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0] and $notetext =~ /\S/;
					},
					success_url => $url,
				) or return;

				die "Shouldn't get here";
			}
		}
	}
	else
	{
		# Set reasonable default values
		$name = $artist->GetName() if ($name eq "");
		$sortname = $artist->GetSortName() if ($sortname eq "");
		$resolution = $artist->GetResolution() if ($resolution eq "");
		$type = $artist->GetType() if ($artist->GetType() and $type == -1); # else we default to 0
		($begindate_y, $begindate_m, $begindate_d) = $artist->GetBeginDateYMD()
			unless grep { $_ } ($begindate_y, $begindate_m, $begindate_d);
		($enddate_y, $enddate_m, $enddate_d) = $artist->GetEndDateYMD()
			unless grep { $_ } ($enddate_y, $enddate_m, $enddate_d);

	}

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit Artist" &>

	<script type="text/javascript" src="/scripts/artistform.js"></script>

	<form method="post" action="/edit/artist/edit.html">

		<& /comp/autofixmenu &>

		<& /comp/layout/editformbegin, title => "Edit Artist", artist => $artist &>

		<table class="formstyle">
			<tr>
				<td colspan="2" class="instructions">
					<ul>
						<li>
							Please read the <& /comp/linkdoc, "StyleGuideline", "Style Guidelines" &>,
							then use the fields below to update the information of
							this artist.</li>
						<li>
							If one or more artists with the same name already exist in the
							database, you will be prompted to add a comment to this artist
							that will allow users to distinguish this entry from the other
							artist(s) with the same name.
							(see <& /comp/linkdoc, "ArtistComment", "Artist Comment" &>)</li>
						<li>
							Please note that casing of the artist is taken into account when
							determining the need for a comment; The two Artists <em>somename</em>
							and <em>Somename</em> are not the same.</li>
					</ul>
				</td>
			</tr>
		</table>

		<table class="formstyle">

			<& /edit/artist/main-form,
				action			=> '/edit/artist/edit.html',
				artist			=> $artist,
				name			=> $name,
				sortname		=> $sortname,
				type			=> $type,
				resolution		=> $resolution,
				begindate		=> [ $begindate_y, $begindate_m, $begindate_d ],
				enddate			=> [ $enddate_y, $enddate_m, $enddate_d ],
				notetext		=> $notetext,

				dupes			=> \@dupes,
				messages		=> \@messages,
				confirm			=> $confirm
			&>

		</table>
		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

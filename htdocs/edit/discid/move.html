<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to add a new artist alias'
	#
	# $Id$
	#
</%perl>
<%args>

	$albumid => ""
	$newalbumid => ""

	$releaseid => undef
	$newreleaseid => undef

	$tocid => ""

	$search => ""

	$confirm => 0
	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	$releaseid ||= $albumid;
	$newreleaseid ||= $newalbumid;

	# only logged in users may see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# check tocid argument
	MusicBrainz::Server::Validation::IsNonNegInteger($tocid) && $tocid
		or return $m->comp("/comp/layout/badarguments",
			text => "Required Argument <strong>tocid</strong> is missing "
				  . "or has  an invalid format.");

	# check tocid argument
	MusicBrainz::Server::Validation::IsNonNegInteger($releaseid) && $releaseid
		or return $m->comp("/comp/layout/badarguments",
			text => "Required Argument <strong>releaseid</strong> is missing "
				  . "or has  an invalid format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object, and load release
	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);

	# Get the cdtoc / release_cdtoc
	my $alcdtoc = MusicBrainz::Server::AlbumCDTOC->newFromAlbumAndCDTOC($mb->{DBH}, $release, $tocid)
		or return $m->comp(
			"/comp/error",
			"No such CDTOC #$tocid, or it is not linked to release #$releaseid",
			1, 1,
		);

	# store references for later use
	my $cdtoc = $alcdtoc->GetCDTOC;
	my $numtracks = $cdtoc->GetTrackCount;

	# we got a form submission to handle
	if ($submitvalue ne "")
	{
		# TODO verify same artist
		# TODO verify same number of tracks
		my $url = "/show/release/?releaseid=$releaseid";

		if ($submitvalue =~ m/Cancel/i)
		{
			return $m->comp("/comp/redirect", $url);
		}
		elsif ($submitvalue =~ m/Enter Edit/i)
		{
			# check newreleaseid argument
			MusicBrainz::Server::Validation::IsNonNegInteger($newreleaseid) && $newreleaseid
				or return $m->comp("/comp/layout/badarguments",
					text => "Required Argument <strong>newreleaseid</strong> is missing "
						  . "or has  an invalid format.");

			my $newrelease = $m->comp("/comp/loadrelease", $mb, $newreleaseid);

			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {
					my @mods = Moderation->InsertModeration(
						DBH	=> $mb->{DBH},
						uid	=> $session{uid},
						privs => $session{privs},
						type => &ModDefs::MOD_MOVE_DISCID,
						# --
						cdtoc => $cdtoc,
						oldalbum => $release,
						newalbum => $newrelease,
					);

					$mods[0]->InsertNote($session{'uid'}, $notetext)
						if $mods[0]
						and $notetext =~ /\S/;
				},
				success_url => $url,
			) or return;

			die "Shouldn't get here";
		}
	}

	# Prepare a string to describe "n tracks"
	my $trackdesc = (
		$numtracks == 1
			? "1 track"
			: "$numtracks tracks"
	);

	# Load the artist of the old release
	my $artist = $m->comp("/comp/loadartist", $mb, $release->GetArtist);

	# Collect the list of releases the user can select the target from
	my @releases;

	if ($search ne "")
	{
		# Do the search, limit to 100 results, user should refine query if
		# there are too many hits.
		my $eng = SearchEngine->new($mb->{DBH}, "album");
		$eng->Search(
			query => $search,
			limit => 100,
		);

		# Build the candidate list from search results
		if ($eng->Rows > 0)
		{
			my @row;
			my $num;

			while (my $row = $eng->NextRow)
			{
				my $newrelease = Album->new($mb->{DBH});
				$newrelease->SetId($row->{'albumid'});

				# Get data for the current candidate
				next if (!defined $newrelease->LoadFromId());
				$num = $newrelease->GetTrackCount();

				# put releases that match the track count into result list
				if ($num == $numtracks)
				{
					push @releases, $newrelease;
				}
			}
		}
	}

	# No search performed, show releases from artist the DiscID belonged to
	else
	{
		if ($release->GetArtist != &ModDefs::VARTIST_ID)
		{
			my @treleases = $artist->GetAlbums(1, 0);
			my $newrelease;
			while ($newrelease = shift @treleases)
			{
				if ($newrelease->{id} != $releaseid && $newrelease->GetTrackCount() == $numtracks)
				{
					push @releases, $newrelease;
				}
			}
		}
	}

	# Sort releases by name
	@releases = map { $_->[0] }
		sort { $a->[1] cmp $b->[1] }
		map {
			my $t = $_;
			use Encode qw( decode );
			use Text::Unaccent qw( unac_string );
			my $n = lc decode "utf-8", unac_string('UTF-8', $t->GetName);
			[ $t, $n ];
		} @releases;

</%perl>


<& /comp/sidebar-notitle, pagetitle => "Move Disc ID" &>

	<& /comp/layout/editformbegin,
		title => "Move Disc ID to another release",
		mp => $alcdtoc->GetModPending,
		mp_type => &ModDefs::MOD_MOVE_DISCID &>

%	if ($newreleaseid ne "")
%	{
%
%		# Load the new release and its artist
%		my $newrelease = $m->comp("/comp/loadrelease", $mb, $newreleaseid);
%		my $newartist = $m->comp("/comp/loadartist", $mb, $newrelease->GetArtist);

		<form method="POST" action="/edit/discid/move.html" id="ConfirmForm">

			<script type="text/javascript" src="/scripts/editsuite.js"></script>

			<table class="formstyle">
				<tr>
					<td class="instructions">
						<ul>
							<li>
								Are you sure you want to move Disc ID

								<strong>
								<& /comp/form/currentvalue,
									alcdtoc => $alcdtoc,
									display => $cdtoc->GetDiscID &>
								</strong>

								from release <& /comp/linkrelease, release => $release &> by
								artist <& /comp/linkartist, artist => $artist &>
								to release <& /comp/linkrelease, release => $newrelease &> by
								artist <& /comp/linkartist, artist => $newartist &>?</li>
						</ul></td>
				</tr>
				<tr class="spaced">
					<td class="notetext">
						<& /comp/notetext, notetext => $notetext &>
						<input type="hidden" name="tocid" value="<% $cdtoc->GetId %>" />
						<input type="hidden" name="releaseid" value="<% $release->GetId %>" />
						<input type="hidden" name="newreleaseid" value="<% $newrelease->GetId %>" /></td>
				</tr>
			</table>

			<& /comp/movefocus, id => "btnYes" &>
		</form>

%	}
%	else
%	{

		<table class="formstyle">
			<tr>
				<td class="instructions">
					<ul>
						<li>
							Select a release to which the Disc ID
							<& /comp/form/currentvalue,
								alcdtoc => $alcdtoc,
								display => $cdtoc->GetDiscID &>

							should be moved to.</li>
						<li>
							Only releases with the <strong>same amount of
							tracks (<% $numtracks %>)</strong> than the
							release the Disc ID is currently attached to
							are shown.</li>
					</ul></td>
			</tr>
		</table>

		<table class="formstyle">
			<tr>
				<td>&nbsp;</td>
				<td>
					If the release you were looking for is not listed below, you can
					try to search for <strong>other releases with <% $trackdesc %></strong>.
					Please note, that the length of the search results is limited to
					the first 100 results. Please provide more detailed search criteria
					until you've found the release you're looking for.</td>
			</tr>
			<tr>
				<td class="label">Search:</td>
				<td class="field">
					<form method="post" action="/edit/discid/move.html">
						<input type="text" name="search" size="25" />
						<input type="submit" value="Search &raquo;" />
						<input type="hidden" name="tocid" value="<% $tocid %>" />
						<input type="hidden" name="releaseid" value="<% $releaseid %>" />
					</form></td>
			</tr>
			<tr>
				<td>&nbsp;</td>
				<td>
					<form method="get" action="/show/release/">
						<input type="hidden" name="releaseid" value="<% $releaseid %>" />
						<input type="submit" value="Cancel" />
					</form></td>
			</tr>
		</table>

	<& /comp/layout/editformend &>

%	# we found some releases
%	if (scalar(@releases) > 0)
%	{
%		my $newrelease;
%		while ($newrelease = shift @releases)
%		{
%			my $newar = Artist->new($mb->{DBH});
%			$newar->SetId($newrelease->GetArtist);
%			$newar->LoadFromId();

		<form method="post" action="/edit/discid/move.html">
			<table width="100%">
				<tr>
					<td>
						Artist: <& /comp/linkartist, artist => $newar &></td>
					<td></td>
				</tr>
				<tr valign="top">
					<td style="width: 420px">
						<& /comp/album,
							artist => $newar, album => $newrelease,
							showlinks => 0, showmodlinks => 0, showrelationships => 0
						&></td>
					<td>
						<input type="submit" name="submitvalue" value="Select &raquo;" />
						<input type="hidden" name="releaseid" value="<% $releaseid %>" />
						<input type="hidden" name="newreleaseid" value="<% $newrelease->GetId() %>" />
						<input type="hidden" name="tocid" value="<% $tocid %>" />
					</td>
				</tr>
			</table>
		</form>

%		}
%	}
%	else
%	{
%		if ($search eq "" && $release->GetArtist != &ModDefs::VARTIST_ID)
%		{
			There were no releases by <& /comp/linkartist, artist => $artist &>
			found to which this Disc ID can be applied to. Please search for releases by different
			artists below.

%		}
%		else
%		{

			There were no releases found. You may refine your search below.

%		}
%	}

	<& /comp/js/diffcollapse &>

%	}


<& /comp/footer &>

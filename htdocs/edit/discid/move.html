%# vi: set ts=4 sw=4 ft=mason :
<%args>
$tocid => ""
$albumid => ""
$newalbumid => ""
$confirm => 0
$notetext => ""
$search => ""
</%args>

<%perl>

MusicBrainz::IsNonNegInteger($tocid) && $tocid
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($albumid) && $albumid
	or return $m->comp("/comp/badargs", 1, 1);

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

my $mb = $m->comp("/comp/dblogin");

my $al = Album->new($mb->{DBH});
$al->SetId($albumid);
$al->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Album not found in the database.",
		1, 1,
	);

# Get the cdtoc / album_cdtoc

my $alcdtoc = MusicBrainz::Server::AlbumCDTOC->newFromAlbumAndCDTOC($mb->{DBH}, $al, $tocid)
	or return $m->comp(
		"/comp/error",
		"No such CDTOC #$tocid, or it is not linked to album #$albumid",
		1, 1,
	);

my $cdtoc = $alcdtoc->GetCDTOC;
my $numtracks = $cdtoc->GetTrackCount;

if ($newalbumid ne "")
{
	MusicBrainz::IsNonNegInteger($newalbumid) && $newalbumid
		or return $m->comp("/comp/badargs", 1, 1);
	
	my $newal = Album->new($mb->{DBH});
	$newal->SetId($newalbumid);
	$newal->LoadFromId
		or return $m->comp(
			"/comp/error",
			"Album not found in the database.",
			1, 1,
		);

	# TODO verify same artist
	# TODO verify same number of tracks

	my $url = "/showalbum.html?albumid=$albumid";

	return $m->comp("move-confirm", $cdtoc, $al, $newal)
		if not $confirm;

	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_MOVE_DISCID,
				# --
				cdtoc => $cdtoc,
				oldalbum => $al,
				newalbum => $newal,
			);

			$mods[0]->InsertNote($session{'uid'}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;
		},
		success_url => $url,
	) or return;

	die "Shouldn't get here";
}

# Prepare a string to describe "n tracks"
my $trackdesc = (
	$numtracks == 1
		? "1 track"
		: "$numtracks tracks"
);

# Load the artist of the old album
my $ar = Artist->new($mb->{DBH});
$ar->SetId($al->GetArtist);
$ar->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Atist not found in the database.",
		1, 1,
	);

# Collect the list of albums the user can select the target from
my @albums;

if ($search ne '')
{	
	# Do the search, limit to 100 results, user should refine query if to many
	my $eng = SearchEngine->new($mb->{DBH}, "album");
	$eng->Search(
		query => $search,
		limit => 100,
	);

	# Build the candidate list from search results
	if ($eng->Rows > 0)
	{
		my @row;
		my $num;

		while (my $row = $eng->NextRow)
		{
			my $newal = Album->new($mb->{DBH});
			$newal->SetId($row->{'albumid'});

			# Get data for the current candidate
			next if (!defined $newal->LoadFromId());
			$num = $newal->GetTrackCount();

			# put albums that match the track count into result list
			if ($num == $numtracks)
			{
				push @albums, $newal;
			}
		}
	} # END Build candidate list
}
else # No search performed, show albums from artist the DiscID belonged to
{
	if ($al->GetArtist != &ModDefs::VARTIST_ID)
	{
		my @talbums = $ar->GetAlbums(1, 0);
		my $newal;
		while ($newal = shift @talbums)
		{ 
			if ($newal->{id} != $albumid && $newal->GetTrackCount() == $numtracks)
			{
				push @albums, $newal;
			}
		}
	}
}

# Sort albums by name
@albums = map { $_->[0] }
	sort { $a->[1] cmp $b->[1] }
	map {
		my $t = $_;
		use Encode qw( decode );
		use Text::Unaccent qw( unac_string );
		my $n = lc decode "utf-8", unac_string('UTF-8', $t->GetName);
		[ $t, $n ];
	} @albums;
</%perl>


%############### Page Layout below ###############

<& /comp/sidebar, title => 'Move Disc ID' &>

%# Build a list of possible target albums

<fieldset>
	<legend>Move Disc ID to another Album</legend>
	<div class="padleft" style="margin-bottom: 0.5em;">
		Select an album to which disc ID
		<a href="/showcdtoc.html?id=<% $cdtoc->GetId %>"><% $cdtoc->GetDiscID %></a> 
		should be moved.<br/>
		NOTE: Only albums with <% $trackdesc %> are shown.
	</div>

%# we found some albums
%if ( scalar(@albums) > 0 )
%{
%	my $newal;
%	while ($newal = shift @albums)
%	{
%		my $newar = Artist->new($mb->{DBH});
%		$newar->SetId($newal->GetArtist);
%		$newar->LoadFromId();
		<div class="padleft">
		<form method="POST" action="move.html"
			enctype="application/x-www-form-urlencoded">
			<table width="100%">
			<tr><td>Artist: 
			<a href="/showartist.html?id=<% $newal->GetArtist %>">
			<% $newar->GetName %></a></td><td></td></tr>
			<tr><td width="100%">
				<& /comp/album, artist=>$newar, album=>$newal, showmodlinks=>0 &>
			</td>
			<td align="right" valign="top">
				<input type="submit" value="Select CD &gt;&gt;">
				<input type="hidden" name="albumid" value="<% $albumid %>">
				<input type="hidden" name="newalbumid" 
					value="<% $newal->GetId() %>">
				<input type="hidden" name="tocid" value="<% $tocid %>">
			</td>
			</tr>
			</table>
		</form>
		</div>
%	}
%}
%else
%{
	<div class="padleft">
%	if ($search eq '' && $al->GetArtist != &ModDefs::VARTIST_ID)
%	{
		There were no albums by 
		<a href="/showartist.html?id=<% $ar->GetId %>"><% $ar->{name} %></a> found
		this DiscId can be applied to. You may search for albums by different
		artists below.
%	} else {
		There were no albums found. You may refine your search below.
%	}
%}
	</div>

%# Search for possible album candidates
	<hr>
	<div class="padleft"><h3>Start another CD search:</h3></div>
	<div class="padleft">
		If the album you were looking for is not in the list above, you can 
		try to search for album titles with this search form.</div>
	<div class="padleft">
		NOTE: Only the first 100 results will be shown in the list of results.
	</div>
	
	<div class="padleft" style="margin-top: 0.5em;">
	<form method="POST" action="move.html" 
		enctype="application/x-www-form-urlencoded">
    	<input type="text" name="search" size="25">
    	<input type="submit" value="Search &gt;&gt;">
    	<input type="hidden" name="tocid" value="<% $tocid %>">
    	<input type="hidden" name="albumid" value="<% $albumid %>">
	</form>
	</div>
</fieldset>

<& /comp/footer &>

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to add a relationship.
	#
	# $Id$
	#
</%perl>
<%args>

	$link0 => -1
	$link1 => -1
	$linktypeid => undef

	$begindate_y => ""
	$begindate_m => ""
	$begindate_d => ""
	$enddate_y => ""
	$enddate_m => ""
	$enddate_d => ""

	$submitvalue => ""
	$notetext => undef
	$swap => ""

</%args>
<%perl>

	# only logged in users may see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# if there are not two elements to relate.
	if ($link0 < 0 || $link1 < 0)
	{
		$m->comp("/comp/layout/badarguments", pagetitle => "Too few elements selected", text => "Please select two elements before you can create a relationship.");
		return undef;
	}

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	my @links = @ARGS{qw( link0 link1 )};
	for my $ref (\(@links))
	{
		my ($type, $id) = $$ref =~ /\A(\w+)=(\d+)\z/
			or die "? $$ref";
		MusicBrainz::Server::LinkEntity->IsValidType($type)
			or die;

		my $obj = MusicBrainz::Server::LinkEntity->newFromTypeAndId(
			$mb->{DBH},
			$type, $id,
		) or die;

		$$ref = {
			type => $type,
			id => $id,
			obj => $obj,
			name => $obj->GetName,
		};
	}

	# Sort the links in the right order
	if ($links[0]->{type} ne $links[1]->{type})
	{
		@links = sort { $a->{type} cmp $b->{type} }  @links;
	}
	elsif ($submitvalue =~ m/Change direction/i)
	{
		my $temp = $links[0];
		$links[0] = $links[1];
		$links[1] = $temp;
	}

	# Sanity check... this should be fine
	my @types = map { $_->{type} } @links;
	MusicBrainz::Server::LinkEntity->ValidateTypes(\@types)
		or die;

	my ($attributes, $desc);
	($linktypeid, $attributes, $desc) = split '\|', $linktypeid if ($linktypeid);

	# create new linktype instance
	my $linkType = MusicBrainz::Server::LinkType->new($mb->{DBH}, \@types);

	# Load the top level link attributes
	my $attrType = MusicBrainz::Server::LinkAttr->new($mb->{DBH});
	my $attr_root = $attrType->Root;
	my @children = $attr_root->Children;


	# prepare validation
	my (@errors, @new_attr_list);
	$ARGS{"v::attrType"} = $attrType;
	$ARGS{"v::attributes"} = $attributes;
	%ARGS = $m->comp("/comp/relationship/checkattributes", %ARGS);
	@new_attr_list = @{ $ARGS{"v::new_attr_list"} };
	@errors = @{ $ARGS{"v::errors"} };

	# if we got a form submission to handle
	if ($submitvalue ne "")
	{
		# handle click of Cancel button
		if ($submitvalue =~ m/cancel/i)
		{
			return $m->comp("/comp/redirect",
				sprintf "/show%s.html?%sid=%s", $links[0]{type}, $links[0]{type}, $links[0]{id});
		}

		elsif ($submitvalue =~ m/Enter Moderation/i)
		{
			# validate
			push @errors, "Please select a relationship type"
				if ($linktypeid !~ /^\d+$/);
			push @errors, "Please enter an valid start date"
				if (!MusicBrainz::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d));
			push @errors, "Please enter an valid end date"
				if (!MusicBrainz::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d));
			push @errors, "The end date needs to be later then the begin date"
				if (!MusicBrainz::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d, $enddate_y, $enddate_m, $enddate_d));
			push @errors, "Please select a subtype of the currently selected relationship type"
				if (!$desc && $linktypeid);

			if (@errors == 0)
			{
				my $newLinkType = $linkType->newFromId($linktypeid);
				if ($newLinkType)
				{

					# Remove the two entities being linked from recent_links
					if (UserPreference::get('remove_recent_link_on_add'))
					{
						my $recent_links = ($session{recent_links} ||= []);
						@$recent_links = grep {
								($_->[0] != $links[0]{type} || $_->[1] != $links[0]{id}) &&
								($_->[0] != $links[1]{type} || $_->[1] != $links[1]{id})
						} @$recent_links;
						$session{recent_links} = $recent_links;
					}

					# So far, always redirects back to the "first" link
					my $typeshow = $links[0]{type} eq "album" ? "release" : $links[0]{type};
					my $url = sprintf "/show/%s/relationships.html?%sid=%s",
								$typeshow,
								$typeshow,
								$links[0]{id};



					$m->comp(
						"/comp/entermods",
						DBH => $mb->{DBH},
						sub => sub {
							my @mods = Moderation->InsertModeration(
								DBH => $mb->{DBH},
								uid => $session{uid},
								privs => $session{privs},
								type => &ModDefs::MOD_ADD_LINK,
								# --
								entities => \@links,
								linktype => $newLinkType,
								begindate => [ $begindate_y, $begindate_m, $begindate_d ],
								enddate => [ $enddate_y, $enddate_m, $enddate_d ],
								attributes => \@new_attr_list,
							);

							$mods[0]->InsertNote($session{'uid'}, $notetext)
								if $mods[0]
								and $notetext =~ /\S/;
						},
						success_url => $url,
					) or return;
    			}
			}
		}
	}



	# form configuration and parameters.
	$ARGS{"v::formurl"} = "/edit/relationship/add.html";

	$ARGS{"v::errors"} = \@errors;
	$ARGS{"v::links"} = \@links;
	$ARGS{"v::begindate"} = [ $begindate_y, $begindate_m, $begindate_d ];
	$ARGS{"v::enddate"} = [ $enddate_y, $enddate_m, $enddate_d ];

	# store root of the relationship tree
	$ARGS{"v::root"} = $linkType->Root;

	# store arguments which might have been modified into args hash again
	$ARGS{"linktypeid"} = $linktypeid;

	$m->comp("/comp/relationship/form", %ARGS);

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

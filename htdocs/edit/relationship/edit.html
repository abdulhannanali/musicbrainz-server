<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to edit a relationship.
	#
	# $Id$
	#
</%perl>
<%args>

	$id => undef
	$type => ""
	$linktypeid => undef

	$begindate_y => ""
	$begindate_m => ""
	$begindate_d => ""
	$enddate_y => ""
	$enddate_m => ""
	$enddate_d => ""

	$submitvalue => ""
	$swapval => 0
	$notetext => undef
	$returnto => 0

</%args>
<%perl>

	# only logged in users may see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	if ($returnto < 0 || $returnto > 1)
	{
		$m->comp("/comp/layout/badarguments", text => "The returnto argument can be either 0 or 1.");
		return undef;
	}

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	if ($type eq "" or !MusicBrainz::Server::Validation::IsNonNegInteger($id))
	{
		$m->comp("/comp/layout/badarguments", text => "The id, type parameters must be provided to this page.");
		return undef;
	}

	my @types = sort split '-', $type;
	my $link = MusicBrainz::Server::Link->new($mb->{DBH}, \@types);
	if (!$link)
	{
		$m->comp("/comp/layout/badarguments", text => "The type argument is invalid.");
		return undef;
	}

	# check the relationship type
	my $node = $link->newFromId($id);
	if (!$node)
	{
		$m->comp("/comp/layout/badarguments", text => "The relationship you selected no longer exists.");
		return undef;
	}

	# Load the relationship type and entities
	my $oldLinkType = $node->LinkType();
	my @entities = $node->Entities();
	my @links;

	# Load the top level relationship attributes
	my $attrType = MusicBrainz::Server::LinkAttr->new($mb->{DBH});

	# Create the links array that contains all the relevant info for the link entities
	push @links, { type => $types[0], id => $entities[0]->GetId(), obj=>$entities[0], name => $entities[0]->GetName() };
	push @links, { type => $types[1], id => $entities[1]->GetId(), obj=>$entities[1], name => $entities[1]->GetName() };

	# Sort the links in the right order
	if ($links[0]->{type} ne $links[1]->{type})
	{
		@links = sort { $a->{type} cmp $b->{type} }  @links;
	}
	my @oldlinks = @links;


	# swapval indicates if the current entities should be reversed
	# (because the user hit the reverse button)
	if ($submitvalue =~ m/Change direction/i)
	{
		$swapval = !$swapval;
	}
	if ($swapval)
	{
		my $temp = $links[0];
		$links[0] = $links[1];
		$links[1] = $temp;
	}

	# trim dates
	($begindate_y, $begindate_m, $begindate_d, $enddate_y, $enddate_m, $enddate_d) = map { s/\s*(.*?)\s*/$1/; $_ }
	($begindate_y, $begindate_m, $begindate_d, $enddate_y, $enddate_m, $enddate_d);

	# These contain the list of attributes
	my @old_attr_list;

	my $attr = MusicBrainz::Server::Attribute->new($mb->{DBH}, \@types);
	$attr = $attr->newFromLinkId($node->GetId);
	if ($attr)
	{
		@old_attr_list = @{ $attr->GetAttributes() };
	}

	# attributes contains the available attributes for the current relationship type.
	# (.e.g. additional=0-1)
	my ($desc, $attributes);
	if ($linktypeid)
	{
		($linktypeid, $attributes, $desc) = split '\|', $linktypeid;
	}
	else
	{
		$linktypeid = $node->GetLinkType();
		$desc = $oldLinkType->GetDescription();
		$attributes = $oldLinkType->GetAttributes();
		foreach my $ref (@old_attr_list)
		{
			for(my $index = 0;; $index++)
			{
				next if (exists $ARGS{"attr_" .$ref->{name} . "_$index"});
				$ARGS{"attr_" . $ref->{name} . "_$index"} = $ref->{value};
				last;
			}
		}
	}

	# prepare validation
	my (@errors, @new_attr_list);
	$ARGS{"v::attrType"} = $attrType;
	$ARGS{"v::attributes"} = $attributes;
	%ARGS = $m->comp("/comp/relationship/checkattributes", %ARGS);
	@new_attr_list = @{ $ARGS{"v::new_attr_list"} };
	@errors = @{ $ARGS{"v::errors"} };

	if ($submitvalue ne "")
	{
		# prepare forward url after action.
		my $type = ($links[$returnto]{type} eq "album" ? "release" : $links[$returnto]{type});
		my $url = "/show/".$type."/?".$type."id=".$links[$returnto]{id};

		# handle click of Cancel button
		if ($submitvalue =~ m/cancel/i)
		{
			return $m->comp("/comp/redirect", $url);
		}
		if ($submitvalue =~ m/Enter Edit/i)
		{
			# validate
			push @errors, "Please select a relationship type"
				if ($linktypeid !~ /^\d+$/);
			push @errors, "Please enter an valid start date"
				if (!MusicBrainz::Server::Validation::IsValidDateOrEmpty($begindate_y, $begindate_m, $begindate_d));
			push @errors, "Please enter an valid end date"
				if (!MusicBrainz::Server::Validation::IsValidDateOrEmpty($enddate_y, $enddate_m, $enddate_d));
			push @errors, "The end date needs to be later then the begin date"
				if (!MusicBrainz::Server::Validation::IsDateEarlierThan($begindate_y, $begindate_m, $begindate_d, $enddate_y, $enddate_m, $enddate_d));
			push @errors, "Please select a subtype of the currently selected relationship type"
				if (!$desc && $linktypeid);

			if (@errors == 0)
			{
				my $newLinkType = $oldLinkType->newFromId($linktypeid);
				if ($newLinkType)
				{
					$m->comp(
						"/comp/entermods",
						DBH => $mb->{DBH},
						sub => sub {
							my @mods = Moderation->InsertModeration(
								DBH            => $mb->{DBH},
								uid            => $session{uid},
								privs          => $session{privs},
								type           => &ModDefs::MOD_EDIT_LINK,
								# --
								newentities    => \@links,
								oldentities    => \@oldlinks,
								newlinktype    => $newLinkType,
								oldlinktype    => $oldLinkType,
								node           => $node,
								begindate      => [ $begindate_y, $begindate_m, $begindate_d ],
								enddate        => [ $enddate_y, $enddate_m, $enddate_d ],
								newattributes  => \@new_attr_list,
								oldattributes  => \@old_attr_list,
							);

							$mods[0]->InsertNote($session{'uid'}, $notetext)
								if $mods[0]
								and $notetext =~ /\S/;
						},
						success_url => $url,
					) or return;
				}
			}

		}
	}
	else
	{
		# prepare dates from relationship
		($begindate_y, $begindate_m, $begindate_d) = $node->GetBeginDateYMD();
		($enddate_y, $enddate_m, $enddate_d) = $node->GetEndDateYMD();
	}

	# form configuration and parameters.
	$ARGS{"v::formurl"} = "/edit/relationship/edit.html";

	$ARGS{"v::errors"} = \@errors;
	$ARGS{"v::links"} = \@links;
	$ARGS{"v::begindate"} = [ $begindate_y, $begindate_m, $begindate_d ];
	$ARGS{"v::enddate"} = [ $enddate_y, $enddate_m, $enddate_d ];

	# store root of the relationship tree
	$ARGS{"v::root"} = $oldLinkType->Root;

	# store arguments which might have been modified into args hash again
	$ARGS{"swapval"} = $swapval;
	$ARGS{"linktypeid"} = $linktypeid;

	$m->comp("/comp/relationship/form", %ARGS);

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

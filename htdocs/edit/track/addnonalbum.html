<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to add a track to special release "non-album tracks"
	#
	# $Id$
	#
</%perl>
<%args>

	$artistid => undef
	$numtracks => 1

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# only logged in users may see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# make sure we got a valid artistid parameter
	MusicBrainz::Server::Validation::IsNonNegInteger($artistid) && $artistid
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>artistid</strong> is missing or has a wrong format.");

	# do not allow if artist is various artists
	$artistid == &ModDefs::VARTIST_ID
		and return $m->comp("/comp/layout/badarguments",
			text => "You cannot add non-album tracks to the VARIOUS_ARTIST artist.");

	# do not allow if artist is deleted artist
	$artistid == &ModDefs::DARTIST_ID
		and return $m->comp("/comp/layout/badarguments",
			text => "You cannot add non-album tracks to the DELETED_ARTIST artist.");

	# Instantiate MusicBrainz object
	my $mb = $m->comp("/comp/dblogin");

	# do not allow new or untrusted users to add non-album tracks
	my $us = UserStuff->newFromId($mb->{DBH}, $session{uid});
	if (($us && $us->IsNewbie()) || UserStuff->IsUntrusted($session{privs}))
	{
		return $m->comp("/comp/layout/error",
			text => "Editors who are members for less then two weeks are not allowed to enter non-album tracks.");
	}

	# make sure we got a valid numtracks parameter
	MusicBrainz::Server::Validation::IsNonNegInteger($numtracks)
		&& $numtracks >= 1
		&& $numtracks <= 100
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>numtracks</strong> is missing or has a wrong format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# load artist from database
	my $artist = $m->comp("/comp/loadartist", $mb, $artistid);

	# fetch array of trackname from arguments
	my @trackname = ();
	my @tracklength = ();
	my @errors = ();

	for (my $i = 1; ; ++$i)
	{
		defined(my $currname = $ARGS{"trackname$i"}) or last;
		$currname =~ /\S/ or last;
		push @trackname, $currname;

		defined(my $currlength= $ARGS{"tracklength$i"}) or last;
		my $lengthasint = Track::UnformatTrackLength($currlength);
		push @tracklength, $lengthasint;
	}

	$numtracks = @trackname;

	# we got a form submission to handle
	if ($submitvalue ne "")
	{
		# if verification above return 0 usable titles
		push @errors, "You have not provided any useable information to add a non-album track!"
			if (not $numtracks);

		# verify form fields
		push @errors, "You must provide an edit note in order to add a non-album track!"
			if ($notetext !~ /\S/);

		# find non-album tracks release, else
		# redirect to artist page after the required
		# action given by submitvalue was performed.
		my $release = Album->new($mb->{DBH});
		my $url = "/artist/".$artist->GetMBId.".html";
		if (my @non = $release->FindNonAlbum($artistid))
		{
			$url = "/release/".$non[0]->GetMBId.".html";
		}

		if ($submitvalue =~ m/Cancel/i)
		{
			$m->comp("/comp/redirect", $url);
		}
		elsif ($submitvalue =~ m/Add more tracks/i)
		{
			$numtracks = 5 * int( ($numtracks + 5) / 5);
		}
		elsif (@errors < 1)
		{
			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {

					for (my $i = 0; $i < $numtracks; $i++)
					{
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_ADD_TRACK_KV,
							# --
							artist => $artist,
							trackname => $trackname[$i],
							tracklength => $tracklength[$i] || 0,
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0]
							and $notetext =~ /\S/;
					}
				},
			) or return;

			$m->comp("/comp/redirect", $url);

			die "Shouldn't get here";
		}
	}

	# show at least one track, even if the validation above
	# did not find any useable information.
	$numtracks ||= 1;

	# replace undefined tracks with empty string "".
	for (@trackname[0..$numtracks-1])
	{
		$_ = "" unless defined;
	}

	# replace undefined lenghts with empty length "?:??".
	for (@tracklength[0..$numtracks-1])
	{
		$_ = 0 unless defined;
	}

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Add Non-Album Tracks" &>

	<form method="post" action="/edit/track/addnonalbum.html" id="AddAlbumTracks">

		<& /comp/autofixmenu &>

		<& /comp/layout/editformbegin, title => "Add Non-Album Tracks" &>

			<table class="formstyle">
				<tr>
					<td class="instructions" colspan="2">
						<ul>
							<li>
								Please enter the name(s) of the track(s) you wish to
								add as non-album tracks of <& /comp/linkartist,
								artist => $artist &>. Blank tracks will be ignored,
								just do not fill anything into the fields you do not
								want to add.</li>
							<li>
								<span style="color: red; font-weight: bold">Important!</span>
								Non-album tracks are designed for <strong>tracks that have
								not been released on ANY release</strong>. Demo tracks and
								promotional tracks that have never been released on
								a release are good candidates for non-album tracks. Most
								live versions or singles are released on <em>single CDs</em>.
								If you want to enter those tracks into MusicBrainz, please
								<a href="/freedb/freedb.html?search=<% uri_escape($artist->GetName()) %>">import
								them from FreeDB</a> instead.</li>
							<li>
								To enter a non-album track you <strong>must</strong> provide
								an edit note that lets the voters know why you think
								this is a non-album track. You're strongly encouraged to
								include a URL that will allow the voters to evaluate the
								correctness of this edit.</li>
						</ul></td>
				</tr>
			<table>

			<table class="formstyle">

% 	if (@errors and $submitvalue =~ m/Enter Edit/i)
%	{
				<tr>
					<td>&nbsp;</td>
					<td class="error" colspan="3">
						<ul>
							<li>
								<%perl> $m->out(join "</li><li>", @errors), </%perl></li>
						</ul></td>
				</tr>
% 	}
				<tr>
					<td class="label">
						<label>Artist:</label></td>
					<td class="field" colspan="3">
						<& /comp/linkartist, artist => $artist &></td>
				</tr>

% 	for (1 .. $numtracks)
%	{

				<tr>
					<td class="label">
						<label for="id_trackname<% $_ %>">Track Name:</label></td>
					<td class="field">
						<input type="text" name="trackname<% $_ %>" id="id_trackname<% $_ %>"
							class="textfield" size="50" value="<% $trackname[$_-1] %>" /></td>
					<td>
						<input type="text" name="tracklength<% $_ %>" class="numberfield"
							size="2" value="<% Track::FormatTrackLength($tracklength[$_-1]) %>" /></td>
					<td>
						<& /comp/form/jsbutton, id=>"BTN_TRACK::trackname".$_ &></td>
				</tr>

% 	}

				<tr>
					<td>&nbsp;</td>
					<td>
						<input type="hidden" name="numtracks" value="<% $numtracks %>" />
						<input class="button" type="submit" name="submitvalue" value="Add more tracks &raquo;" /></td>
					<td colspan="2">

% 	if ($numtracks > 1)
%	{

						<& /comp/form/jsbutton, id=>"BTN_ALL" &>

% 	}

					</td>
				</tr>
			<table>

			<table class="formstyle">
				<tr>
					<td class="label">&nbsp;</td>
					<td class="notetext">
						<& /comp/notetext,
							notetext => $notetext,
							required => 1, &>

						<input type="hidden" name="artistid" value="<% $artistid %>" /></td>
				</tr>
			</table>

			<& /comp/movefocus, id => "id_trackname1" &>

		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

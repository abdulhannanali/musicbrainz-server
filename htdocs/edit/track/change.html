<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to change the track title, and track artist of a
	# track.
	#
	# $Id$
	#
</%perl>
<%args>

	$album => ""
	$releaseid => ""
	$tindex => 0
	$modid => 0
	$solo => 0
	$trackname => ""

	# fields set by /comp/artistfilter
	$artistfilter_newartistid => ""
	$newartistid => ""
	$search => ""

	$submitvalue => ""
	$notetext => ""

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# backwards compability
	$releaseid ||= $album;

	# make sure we got a valid releaseid parameter
    MusicBrainz::Server::Validation::IsNonNegInteger($releaseid) && $releaseid
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>releaseid</strong> is missing or has a wrong format.");

	# make sure we got a valid tindex parameter
    MusicBrainz::Server::Validation::IsNonNegInteger($tindex)
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>tindex</strong> is missing or has a wrong format.");

	# make sure we got a valid modid parameter
    MusicBrainz::Server::Validation::IsNonNegInteger($modid)
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>modid</strong> is missing or has a wrong format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object, and load entities
	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);
	my $artist = $m->comp("/comp/loadartist", $mb, $release->GetArtist);
	my @tracks = $release->LoadTracks();


	# Check if we've done all the tracks on this release
	if ($tindex >= scalar(@tracks))
	{
		</%perl>

		<& /comp/sidebar-notitle, pagetitle => "Release has been converted to multiple artists" &>

			<& /comp/tablebegin, title => "Release has been converted to multiple artists" &>

				The edits for the change of release <& /comp/linkrelease,
				release => $release &> by <& /comp/linkartist, artist =>
				$artist &> a to multiple artists release have been entered. Thanks!

			<& /comp/tableend &>

		<& /comp/footer &>

		<%perl>
		return;
	}

	# load newartist from database if it has been set by artistfilter
	my $newartist = undef;
	$artistfilter_newartistid ||= $newartistid;
	if ($artistfilter_newartistid ne "")
	{
		$newartist = $m->comp("/comp/loadartist", $mb, $artistfilter_newartistid);
		$newartistid = $newartist->GetId;
	}

	# load track artist
	my $track = $tracks[$tindex];
	my $trackartist = $m->comp("/comp/loadartist", $mb, $track->GetArtist);

	# prepare validation errors
	my @errors = ();

	# we got a form submission to handle
	if ($submitvalue ne "")
	{
		# If the user had to click "swap", flip our "split order" flag
		$session{"changetrack_splitorder"} = not $session{"changetrack_splitorder"}
			if $ARGS{'swapped'};

		# check track name
		push @errors, "Please enter a non-empty track title"
			if (not MusicBrainz::Server::Validation::IsNonEmptyString($trackname));

		# prepare url for redirection back to the release page
		my $releaseurl = "/release/".$release->GetMBId.".html";

		if ($submitvalue =~ m/Cancel/i)
		{
			return $m->comp("/comp/redirect", $releaseurl);
		}

		# important: pressing the Change artist button or the
		# search button has to clear the selected artist possibly
		# selected before (or the first of the list of found artists
		# in the search results if the search was triggered
		# automatically).
		elsif ($submitvalue =~ m/Change artist|Search/i)
		{
			$newartist = undef;
			$newartistid = undef;
		}
		elsif ($submitvalue =~ m/Add New Artist/i)
		{
			# prepare redirect for add artist form
			my $returnurl = "/edit/track/change.html";
			my %returnargs;
			$returnargs{"releaseid"} = $releaseid;
			$returnargs{"tindex"} = $tindex;
			$returnargs{"modid"} = $modid;
			$returnargs{"solo"} = $solo;
			$returnargs{"trackname"} = $trackname;
			$returnurl = sprintf "$returnurl?%s&artistfilter_newartistid=", $m->comp("/comp/args-to-querystring.inc", %returnargs);
			$returnurl = uri_escape($returnurl);
			$search = uri_escape($search);

			my $addartisturl = "/edit/artist/add.html?name=$search&returnurl=$returnurl";
			return $m->comp("/comp/redirect", $addartisturl);
		}

		elsif ($submitvalue =~ m/Enter edit/i and
			   not @errors)
		{

			#die($trackname."--".$track->GetName."--".$trackartist->GetId."--".$newartist->GetId);

			# Enter edit track name edit if it was changed
			if ($trackname ne $track->GetName)
			{
				$m->comp(
					"/comp/entermods",
					DBH => $mb->{DBH},
					sub => sub {
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_TRACKNAME,
							# --
							track => $track,
							newname => $trackname,
							# TODO depmod on $modid ?
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0]
							and $notetext =~ /\S/;
					},
				) or return;
			}

			# enter change track artist edit if it was changed
			if ($trackartist->GetId != $newartist->GetId)
			{
				$m->comp(
					"/comp/entermods",
					DBH => $mb->{DBH},
					sub => sub {
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_CHANGE_TRACK_ARTIST,
							# --
							track => $track,
							oldartist => $trackartist,
							artistid => $newartist->GetId,
							artistname => $newartist->GetName,
							artistsortname => $newartist->GetSortName,
							# TODO depmod on $modid ?
						);

						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0]
							and $notetext =~ /\S/;
					},
				) or return;
			}

			# redirect to next change.html page if solo=0,
			# else back to the release page.
			my $url = $solo
				? $releaseurl
				: "/edit/track/change.html?releaseid=$releaseid&tindex=${\ ($tindex + 1) }&modid=$modid";
			return $m->comp("/comp/redirect", $url);

			die "Shouldn't get here";
		}
	}


	# Scan through the track titles and artists, and work out what the likely
	# "split" sequence is, and what the most common track artist is.
	my %trackartists;
	my %split = map { $_ => 0 } (" - ", "-", " / ", "/", ": ", ":");

	for my $t (@tracks)
	{
		for my $split (keys %split)
		{
			++$split{$split} if index($t->GetName, $split) >= 0;
			++$trackartists{$t->GetArtist};
		}
	}

	# Prefer longer split sequences (e.g. " - " over "-")
	# then sort by frequency.  e.g. one " - " beats three "-".
	my $split = do {
		my @k = sort {
			length($a) <=> length($b)
			or
			$split{$a} <=> $split{$b}
		} grep {
			$split{$_}
		} keys %split;
		$k[-1];
	};
	my $mostcommonartist = (sort { $trackartists{$a} <=> $trackartists{$b} } keys %trackartists)[-1];

	# These are the existing values
	my ($oldtrackname, $oldartistname) = ($track->GetName, $trackartist->GetName);

	# Do a split on the track name
	my ($splittrackname, $splitartistname) = split($split, $oldtrackname, 2)
		if defined $split;

	my $did_split = defined $splitartistname;

	($splittrackname, $splitartistname) = ($splitartistname, $splittrackname)
		if $session{'changetrack_splitorder'};

	# Trim everything
	for ($oldtrackname, $oldartistname, $splittrackname, $splitartistname)
	{
		s/\A\s+//;
		s/\s+\z//;
		s/\s\s+/ /g; #/# for vim
	}

	# Only set the values if they were not modified, or
	# the form was submitted.
	if ($trackname eq "")
	{
		# Now work out what we think is the more likely - use the split values,
		# or use the current values.
		($trackname, $search) = ($oldtrackname, $oldartistname);

		if ($did_split and (
				$track->GetName eq $artist->GetName or
				$track->GetName eq $trackartist->GetName or
				$track->GetArtist == $release->GetArtist or
				$track->GetArtist == $mostcommonartist))
		{
			$trackname = $splittrackname;
			$search = $splitartistname;
		}
	}

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Change track title and artist" &>

% 	unless ($solo)
%	{

	<p>Track <% $tindex+1 %> of <% scalar @tracks %></p>

% 	}


	<form method="post" action="/edit/track/change.html" id="ChangeTrackForm">

		<& /comp/autofixmenu &>

		<& /comp/layout/editformbegin, title => "Change track title and artist", track => $track &>

		<table class="formstyle">
			<tr>
				<td class="instructions" colspan="2">
					<ul>
						<li>
							Please read the <& /comp/linkdoc, "StyleGuideline", "style guidelines" &>,
							and review the documentation about <& /comp/linkdoc, "EditTrackNameEdit",
							"editing track titles" &> and <& /comp/linkdoc, "ChangeTrackArtistEdit",
							"changing track artists" &>, before entering this edit.</li>
						<li>
							When you are ready, you can modify the track title,
							and/or search for the artist to which you would
							like to move this track.</li>
					</ul></td>
			</tr>
		</table>

		<table class="formstyle">

% 	if (@errors and $submitvalue =~ m/Enter edit/i)
%	{

			<tr>
				<td>&nbsp;</td>
				<td class="error">
					<ul>
						<li>
							<%perl> $m->out(join "</li><li>", @errors), </%perl></li>
					</ul></td>
			</tr>

% 	}

			<tr>
				<td class="label">
					<label>Current title:</label></td>
				<td>
					<& /comp/form/currentvalue, track => $track, display => $oldtrackname &></td>
			</tr>
			<tr>
				<td class="label">
					<label>Current artist:</label></td>
				<td>
					<& /comp/form/currentvalue, artist => $trackartist &></td>
			</tr>

			<tr class="spaced">
				<td class="label">
					<label for="id_trackname">New title:</label></td>
				<td>
					<input type="text" name="trackname"	class="textfield"
						id="id_trackname" size="50" maxlength="255"
						 value="<% $trackname %>" />
					<& /comp/form/jsbutton, id=>"BTN_TRACK::trackname" &></td>
			</tr>


% 	if (not defined $newartist)
%	{

			<tr>
				<td class="label">&nbsp;</td>
				<td>

					<& /comp/form/jsbutton, id => "BTN_USESWAP::search::trackname::swapped" &>
					<& /comp/form/jsbutton, id => "BTN_USECURRENT" &>

% 		if ($did_split)
%		{

					<& /comp/form/jsbutton, id => "BTN_USESPLIT" &>

%		}

					<& /comp/form/jsbutton, id => "BTN_GUESSBOTH::search::trackname" &>

					<input type="reset" value="Use Guessed" id="BTN_USEGUESSED"  />

%# the following fields are used for the javascript buttons
					<input type="hidden" name="swapped" value="0" />
					<input type="hidden" name="orig_artname" value="<% $trackartist->GetName %>" />
					<input type="hidden" name="orig_track" value="<% $track->GetName %>" />
%

% 		if ($did_split)
%		{

					<input type="hidden" name="split_artname" value="<% $splitartistname %>" />
					<input type="hidden" name="split_track" value="<% $splittrackname %>" />

% 		}

				</td>
			</tr>

			<& /comp/artistfilter,
				search => $search,
				defaultquery => $oldartistname,
				submitvalue => $submitvalue,
			&>

% 	}
%	else
%	{

			<tr class="top">
				<td class="label">New artist:</td>
				<td>
					<& /comp/linkartist, artist => $newartist &>
					<input type="hidden" name="newartistid" value="<% $newartist->GetId %>" />
					<div style="margin-top: 2px">
						<input type="submit" name="submitvalue" value="Change artist &raquo;" />
					</div></td>
			</tr>
			<tr>
				<td>&nbsp;</td>
				<td class="notetext">
					<& /comp/notetext,
						notetext => $notetext, &></td>
			</tr>

%	}

		</table>

% 	if (not defined $newartist)
%	{

		<input type="hidden" name="newartistid" value="<% $trackartist->GetId %>" />
%	}

		<& /comp/form/hiddenfield, "releaseid", $releaseid &>
		<& /comp/form/hiddenfield, "tindex", $tindex &>
		<& /comp/form/hiddenfield, "modid", $modid &>
		<& /comp/form/hiddenfield, "solo", $solo ? 1 : 0 &>
		<& /comp/form/focusfield, "id_trackname" &>

		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows to change the track name, number and length of a track
	#
	# $Id$
	#
</%perl>
<%args>

	$album => undef
	$releaseid => undef

	$track => undef
	$trackid => undef

	$newtrackname => ""
	$newseq => ""
	$newlength => ""

	$submitvalue => ""
	$notetext => ""

	# it is possible to set new values for the fields when
	# calling this page (e.g. from reports?)
	$defaultname => undef
	$defaultseq => undef
	$defaultlength => undef

</%args>
<%perl>

	# only logged in users can see this page
	$m->comp("/comp/checkloggedin", 1, 1)
		or return;

	# backwards compability
	$releaseid ||= $album;
	$trackid ||= $track;

	# make sure we got a valid releaseid parameter
    MusicBrainz::Server::Validation::IsNonNegInteger($releaseid) && $releaseid
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>releaseid</strong> is missing or has a wrong format.");

	# make sure we got a valid trackid parameter
    MusicBrainz::Server::Validation::IsNonNegInteger($trackid)
		or return $m->comp("/comp/layout/badarguments",
			text => "The argument <strong>trackid</strong> is missing or has a wrong format.");

	# check volatile preferences (like autoeditor flag)
	$m->comp("/comp/user/check-volatile-preferences", %ARGS);

	# Instantiate MusicBrainz object, and load entities
	my $mb = $m->comp("/comp/dblogin");
	my $release = $m->comp("/comp/loadrelease", $mb, $releaseid);
	my $artist = $m->comp("/comp/loadartist", $mb, $release->GetArtist);
	$track = $m->comp("/comp/loadtrack", $mb, $trackid, $releaseid);

	# Allow the track number to be set as high as either the highest track number,
	# or the number of tracks on the release, whichever is the greater.
	my $last_seq = 0;
	if (my $tracks = $release->GetTracks)
	{
		my $track_cnt = $release->GetTrackCount;
		$last_seq = (@$tracks ? $tracks->[-1]->GetSequence : 1);
		$last_seq = $track_cnt if ($track_cnt > $last_seq);
	}

	# prepare validation errors
	my @errors = ();

	# we got a form submission to handle
	if ($submitvalue ne "")
	{
		# check new track sequence
		if (not $release->IsNonAlbumTracks and
			MusicBrainz::Server::Validation::IsNonNegInteger($newseq))
		{
			push @errors, "New track number too low (lowest allowed track number is 1)" if ($newseq == 0);
			push @errors, "New track number too high (highest allowed track number is $last_seq)" if ($newseq > $last_seq);
		}

		# check new track name
		push @errors, "Please enter a non-empty track name"
			if (not MusicBrainz::Server::Validation::IsNonEmptyString($newtrackname));
		push @errors, "The new track name has an invalid format (newline found)"
			if (not MusicBrainz::Server::Validation::IsSingleLineString($newtrackname));

		# check new track length
		$newlength = Track::UnformatTrackLength($newlength);
		push @errors, "The new track length has an invalid format (either ?:?? or a valid length in minutes:seconds is required)"
			if (not MusicBrainz::Server::Validation::IsNonNegInteger($newlength));

		# prepare url for redirection back to the release page
		my $releaseurl = "/release/".$release->GetMBId.".html";

		if ($submitvalue =~ m/Cancel/i)
		{
			return $m->comp("/comp/redirect", $releaseurl);
		}
		elsif (@errors < 1)
		{
			# Don't redirect if we're in a popup window.
			my %redirect;
			$redirect{success_url} = $releaseurl unless $pnotes{'ispopup'};

			$m->comp(
				"/comp/entermods",
				DBH => $mb->{DBH},
				sub => sub {

					# only enter MOD_EDIT_TRACKNAME if the trackname
					# was modified
					if ($newtrackname ne $track->GetName)
					{
						my @mods = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_TRACKNAME,
							# --
							track => $track,
							newname => $newtrackname,
						);
						$mods[0]->InsertNote($session{'uid'}, $notetext)
							if $mods[0]
							and $notetext =~ /\S/;
					}

					# only enter MOD_EDIT_TRACKNUM if the track number
					# was modified
					if (not $release->IsNonAlbumTracks and
						$newseq != $track->GetSequence)
					{
						my @mods2 = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_TRACKNUM,
							# --
							track => $track,
							newseq => $newseq,
						);
						$mods2[0]->InsertNote($session{'uid'}, $notetext)
							if $mods2[0]
							and $notetext =~ /\S/;
					}

					# only enter MOD_EDIT_TRACKTIME if the track length
					# was modified
					if (Track::FormatTrackLength($newlength) ne
						Track::FormatTrackLength($track->GetLength))
					{
						my @mods3 = Moderation->InsertModeration(
							DBH	=> $mb->{DBH},
							uid	=> $session{uid},
							privs => $session{privs},
							type => &ModDefs::MOD_EDIT_TRACKTIME,
							# --
							track => $track,
							newlength => $newlength,
						);
						$mods3[0]->InsertNote($session{'uid'}, $notetext)
							if $mods3[0]
							and $notetext =~ /\S/;
					}
				},
				%redirect,
			) or return;

			die "Shouldn't get here" unless $pnotes{'ispopup'};

</%perl>
			<& /comp/header_small, title => "Edits entered" &>
				<script type="text/javascript"> window.close(); </script>
				<p>Edits entered successfully.</p>
				<p><a href="/">Continue</a></p>
			<& /comp/footer_small &>
<%perl>

			return;
		}
	}

	# if the release has DiscIDs attached, no tracks can be added nor removed.
	my $can_add_remove = ($release->CanAddTrack or $release->CanRemoveTrack);

</%perl>

<& /comp/sidebar-notitle, pagetitle => "Edit Track Number, Name and Time" &>


	<form method="post" action="/edit/track/edit.html" id="EditTrackForm">

		<& /comp/autofixmenu &>

		<& /comp/layout/editformbegin, title => "Edit Track Number, Name and Time",
			track => $track, mp_type => &ModDefs::MOD_EDIT_TRACKNAME &>

			<table class="formstyle">
				<tr>
					<td>&nbsp;</td>
					<td><b>#:</b></td>
					<td><b>Name:</b></td>
					<td><b>Time:</b></td>
					<td>&nbsp;</td>
				</tr>
				<tr>
					<td class="label">
						<label>Current:</label></td>

% 	unless ($release->IsNonAlbumTracks)
%	{

					<td>
						<div class="oldtextfield"><% $track->GetSequence  %></div></td>

% 	}
%	else
%	{

					<td>&nbsp;</td>

% 	}

					<td>
						<& /comp/form/currentvalue, track => $track &></td>
					<td>
						<div class="oldlength"><% Track::FormatTrackLength($track->GetLength) %></div></td>

					<td>&nbsp;</td>
				</tr>
				<tr>
					<td class="label">
						<label for="id_newtrackname">New:</label></td>

% 	unless ($release->IsNonAlbumTracks)
%	{

					<td>
						<input type="text" name="newseq" class="numberfield"
							<% $can_add_remove ? "" : 'readonly="readonly"' |n %>
							title="Set a new track number here, possible values are: (1-<% $last_seq %>)"
							size="2" value="<% $defaultseq || $track->GetSequence %>" /></td>

% 	}
%	else
%	{

					<td>&nbsp;</td>

% 	}

					<td>
						<input type="text" name="newtrackname" class="textfield"
							id="id_newtrackname" size="50" maxlength="255"
							value="<% $defaultname || $track->GetName %>" /></td>
					<td>
						<input type="text" size="4" class="numberfield"
							<% $can_add_remove ? "" : 'readonly="readonly"' |n %>
							name="newlength" value="<% $defaultlength || Track::FormatTrackLength($track->GetLength) %>" /></td>
					<td>
						<& /comp/form/jsbutton, id=>"BTN_TRACK::newtrackname" &></td>
				</tr>

% 	unless ($release->IsNonAlbumTracks)
%	{

				<tr>
					<td>&nbsp;</td>
					<td colspan="4"><small>Valid Track Numbers: 1-<% $last_seq %></small></td>
				</tr>

% 	}

				<tr>
					<td>&nbsp;</td>
					<td class="notetext" colspan="4">
						<& /comp/notetext,
							notetext => $notetext, &>

						<input type="hidden" name="releaseid" value="<% $releaseid %>" />
						<input type="hidden" name="trackid" value="<% $trackid %>" />
						<input type="hidden" name="ispopup" value="<% $pnotes{'ispopup'} %>" />
						<& /comp/form/focusfield, "id_newtrackname" &>	</td>
				</tr>
			</table>

		<& /comp/layout/editformend &>

	</form>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

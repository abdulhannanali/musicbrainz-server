%# vi: set ts=4 sw=4 ft=mason :
<%args>
@id => ()
$untag => 0
$attr_type => &Album::ALBUM_ATTR_ALBUM
$attr_status => &Album::ALBUM_ATTR_OFFICIAL
</%args>
<%perl>

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

for my $album (@id)
{
	MusicBrainz::IsNonNegInteger($album) && $album
		or return $m->comp("/comp/badargs", 1, 1);
}

my $mb = $m->comp("/comp/dblogin");

my @objs;
my %artists;

for my $album (@id)
{
	my $al = Album->new($mb->{DBH});
	$al->SetId($album);

	$al->LoadFromId
		or return $m->comp(
			"/comp/error",
			"Album #$album does not exist",
			1, 1,
		);

	push @objs, $al;
	++$artists{$al->GetArtist};
}

my $modpending = grep { $_->GetAttributeModPending } @objs;

my $selected_index = 0;

my $url =
	@id == 1
		? "/showalbum.html?albumid=$id[0]"
		: keys(%artists) == 1
			? "/showartist.html?artistid=".$objs[0]->GetArtist
			: "/search.html";

if ($ARGS{confirm})
{
	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => ModDefs::MOD_EDIT_ALBUMATTRS,
				# --
				albums => \@objs,
				attrs => [ $attr_type, $attr_status ],
			);

			delete $session{'tagged_albums'}
				if $untag;
		},
		success_url => $url,
	) or return;

	die "Shouldn't get here";
}

my %attrs;

for my $al (@objs)
{
	++$attrs{$_}
		for $al->GetAttributes;
}

my @attrs = sort { $a<=>$b } keys %attrs;

</%perl>

<& /comp/sidebar, title => 'Edit Album Attributes' &>

% if ($modpending) {
    <& /comp/modpending &>
% } 

<form method="post" action="/editalbumattrs.html"
	name="EditAlbumAttrsForm"
	>

	<& /comp/albumattrform,
		mb			=> $mb,
		albumname	=> (@objs > 1 ? "" : $objs[0]->GetName),
		attrs		=> \@attrs,
		field1		=> "attr_type",
		field2		=> "attr_status",
		selindex	=> \$selected_index,
	&>

% for (@id) {
	<input type="hidden" name="id" value="<% $_ %>">
% }
	<input type="hidden" name="untag" value="<% $untag %>">
	<input type="hidden" name="confirm" value="1">
</form>

<& /comp/movefocus, "EditAlbumAttrsForm", "value0", $selected_index &>
<& /comp/footer &>

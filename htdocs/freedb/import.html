%# vi: set ts=4 sw=4 ft=mason :
<%args>

	$catid => ""

</%args>
<%perl>

	my ($cat, $id) = $catid =~ /
		^\s*				# start of line, leading space
		(\w*)				# category (e.g. misc)
		(?:					# either:
			\s*\/\s*		#  a slash (optional whitespace)
			|				# or:
			\s+				#  mandatory whitespace
		)
		([0-9A-Fa-f]{8})	# data ID (e.g. 12345678)
		\s*$				# trailing space, end of line
		/x;

	return $m->comp("id-error", catid => $catid)
		if not defined $cat;

	my $mb = $m->comp("/comp/dblogin");

	my $fdb = FreeDB->new($mb->{DBH});
	my $lookup = $fdb->LookupByFreeDBId($id, $cat);

	return $m->comp("not-found", catid => $catid)
		if not defined $lookup;

	# Save the original data lookup results
	my %args;
	my $t = $args{"tracks"} = @{ $lookup->{tracks} };

	# $lookup has keys:
	# - argsid (eq $id)
	# - argscat (eq $cat)
	# - artist (string)
	# - sortname (??? is this meant to be in there?)
	# - album (string)
	# - tracks: array ref, each entry is hash of {tracknum, track}
	# - durations: space-joined list of integers, one per track

	# Let's start off with some trivial args tidying
	my $tidy = sub {
		local $_ = shift;
		use Encode qw( encode decode );
		$_ = decode "utf-8", $_;
		s/\A\s+//;
		s/\s+\z//;
		s/\s+/ /g; #/#
		tr/\x{0060}\x{00B4}"\x{00AB}\x{00BB}/'/; #"#
		encode "utf-8", $_;
	};

	$args{"artistname"} = &$tidy($lookup->{"artist"});
	$args{"releasename"} = &$tidy($lookup->{"album"});

	# Hmmm, does args always return 1..n tracks (no gaps?)
	# i.e. do we ignore "tracknum" ?
	for my $i (0 .. $t-1)
	{
		my $n = &$tidy($lookup->{"tracks"}[$i]{"track"});

		# Remove leading track number
		my $n1 = $i+1;
		my $n2 = sprintf "%02d", $n1;
		$n =~ s/^($n1|$n2)\b[\.\)]?\s*//;
		$args{"track$i"} = $n;
	}
	$args{"freedbcat"} = $lookup->{"freedbcat"};
	$args{"freedbid"} = $lookup->{"freedbid"};
	$args{"durations"} = $lookup->{"durations"};

	$args{"offer_charset"} = 1;
	$args{"ready"} = 1;

	# First question: to VA or not to VA?
	return $m->comp("/freedb/select-macsac.html", %args);
</%perl>

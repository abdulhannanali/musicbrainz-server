%# vi: set ts=2 sw=2 ft=mason :
<%args>
$album => undef
</%args>
<%perl>

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

MusicBrainz::IsNonNegInteger($album) && $album
	or return $m->comp("/comp/badargs", 1, 1);

my $mb = $m->comp("/comp/dblogin");

my $al = Album->new($mb->{DBH});
$al->SetId($album);
$al->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Album #$album does not exist",
		1, 1,
	);

$al->GetArtist == &ModDefs::VARTIST_ID
	and return $m->comp(
		"/comp/error",
		"Sorry - so far, the 'Edit Album' feature does not work for Various Artist albums.",
		1, 1,
	);

$al->IsNonAlbumTracks
	and return $m->comp(
		"/comp/error",
		"The 'Edit Album' feature cannot edit '".&Album::NONALBUMTRACKS_NAME."'",
		1, 1,
	);

my $ar = Artist->new($mb->{DBH});
$ar->SetId($al->GetArtist);
$ar->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Artist does not exist",
		1, 1,
	);

my @t = $al->LoadTracks;

@t = map {
	+{
		id				=> $_->GetId,
		name			=> $_->GetName,
		seq				=> $_->GetSequence,
		orig_name	=> $_->GetName,
		orig_seq	=> $_->GetSequence,
	},
} @t;

my ($type, $status) = $al->GetReleaseTypeAndStatus;

my %edit = (
	id				=> $al->GetId,
	name			=> $al->GetName,
	type			=> $type,
	status		=> $status,
	orig_name	=> $al->GetName,
	orig_type	=> $type,
	orig_status=> $status,
	artist		=> {
		id				=> $ar->GetId,
		name			=> $ar->GetName,
		sortname	=> $ar->GetName,
	},
	tracks		=> \@t,
);

$session{editalbum} = \%edit;

#$m->comp("/user/debug.html");

return $m->comp("/comp/redirect", "/mod/edit-album/sac-edittracks.html");

</%perl>

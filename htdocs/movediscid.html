%# vi: set ts=4 sw=4 ft=mason :
<%args>
$discid => ""
$albumid => ""
$newalbumid => ""
$confirm => 0
$notetext => ""
</%args>
<%perl>

$m->comp("/comp/checkloggedin", 1, 1)
	or return;

MusicBrainz::IsSingleLineString($discid) && length($discid)
	or return $m->comp("/comp/badargs", 1, 1);

MusicBrainz::IsNonNegInteger($albumid) && $albumid
	or return $m->comp("/comp/badargs", 1, 1);

my $mb = $m->comp("/comp/dblogin");

my $al = Album->new($mb->{DBH});
$al->SetId($albumid);
$al->LoadFromId
	or return $m->comp(
		"/comp/error",
		"Album not found in the database.",
		1, 1,
	);

# Get the ID/toc of the row that joins this discid to this album

my $di = Discid->new($mb->{DBH});
my @discid_refs = $di->GetDiscidFromAlbum($al->GetId)
	or return $m->comp(
		"/comp/error",
		"Invalid discid and album passed.",
		1, 1,
	);

(my $d) = grep { $_->{discid} eq $discid } @discid_refs
	or return $m->comp(
		"/comp/error",
		"Invalid discid and album passed.",
		1, 1,
	);

my $id = $d->{id};
my $numtracks = 0;
$numtracks = $1
	if $d->{toc}
	and $d->{toc} =~ /^\d+ (\d+)/;

if ($newalbumid ne "")
{
	MusicBrainz::IsNonNegInteger($newalbumid) && $newalbumid
		or return $m->comp("/comp/badargs", 1, 1);
	
	my $newal = Album->new($mb->{DBH});
	$newal->SetId($newalbumid);
	$newal->LoadFromId
		or return $m->comp(
			"/comp/error",
			"Album not found in the database.",
			1, 1,
		);

	# TODO verify same artist
	# TODO verify same number of tracks

	my $url = "/showalbum.html?albumid=$albumid";

	$m->comp(
		"/comp/entermods",
		DBH => $mb->{DBH},
		sub => sub {
			my @mods = Moderation->InsertModeration(
				DBH	=> $mb->{DBH},
				uid	=> $session{uid},
				privs => $session{privs},
				type => &ModDefs::MOD_MOVE_DISCID,
				# --
				oldalbum => $al,
				newalbum => $newal,
				discid => $discid,
				rowid => $id,
			);

			# TODO no UI for entering $notetext
			$mods[0]->InsertNote($session{'uid'}, $notetext)
				if $mods[0]
				and $notetext =~ /\S/;
		},
		success_url => $url,
	) or return;

	die "Shouldn't get here";
}

# Build a list of possible target albums

my $ar = Artist->new($mb->{DBH});
$ar->SetId($al->GetArtist);

my @albums = $ar->GetAlbums
	or return $m->comp(
		"/comp/error",
		"Cannot load albums for artist.",
		1, 1,
	);

@albums = grep {
	my $t = $_;

	$t->GetId != $al->GetId
	and not $t->IsNonAlbumTracks
	and $t->GetArtist != &ModDefs::VARTIST_ID
	and do {
		# TODO is this really a good way of checking the number of tracks?
		# Shouldn't we test the album's TOC, not the number of defined tracks?
		scalar $t->LoadTracks;
	} == $numtracks
} @albums;

# Sort albums by name
@albums = map { $_->[0] }
	sort { $a->[1] cmp $b->[1] }
	map {
		my $t = $_;
		use Encode qw( decode );
		use Text::Unaccent qw( unac_string );
		my $n = lc decode "utf-8", unac_string('UTF-8', $t->GetName);
		[ $t, $n ];
	} @albums;

# Prepare a string to describe "n tracks"
my $trackdesc = (
	$numtracks == 1
		? "1 track"
		: "$numtracks tracks"
);

</%perl>

<& /comp/sidebar, title => 'Move Discid' &>

% if (@albums) {

<p>
	Select a new album for CD Index Id 
	<span class="bold"><% $discid %></span>
	(NOTE: Only albums with <% $trackdesc %> have been shown).
<p>

<table>

%     for my $ref (@albums) {
	<tr>
		<td>
			<& /comp/album, artist=>$ar, album=>$ref, nomod=>1 &>
		</td>
		<td>
            <form method="post" action="/movediscid.html"
				style="margin: 1em"
				enctype="application/x-www-form-urlencoded"
				class="formstyle"
				>
				<p>
					<input type="hidden" name="discid" value="<% $discid %>">
					<input type="hidden" name="albumid" value="<% $albumid %>">
					<input type="hidden" name="newalbumid" value="<% $ref->GetId %>">
					<input type="submit" value="Select &gt;&gt;">
				</p>
			</form>
		</td>
	</tr>

	<tr><td>&nbsp;</td></tr>

% }
</table>

% } else {

	<p>
		Disc IDs can only be moved to albums with the same number
		of tracks.&nbsp;
		Since this artist does not have any other albums with <% $trackdesc %>,
		disc id <% $discid %> cannot be moved.
	</p>
	<p>
		<a href="/showalbum.html?albumid=<% $al->GetId %>">Back</a>
		to album
		'<% $al->GetName() %>'.
	</p>

% }

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :
<%args>

	$artist => undef

</%args>
<%perl>

my ($id, $mbid);
if (MusicBrainz::IsNonNegInteger($artist)) { $id = $artist }
elsif (MusicBrainz::IsGUID($artist)) { $mbid = $artist }
else { return $m->comp("/comp/badargs", 1, 1) }

return $m->comp("/comp/error", "Public subscription lists are not released until Tue Mar 15 20:30:00 2005 UTC", 1, 1)
	unless time() >= 1110918600;

my $mb = $m->comp("/comp/dblogin");

$artist = Artist->new($mb->{DBH});
$artist->SetMBId($mbid) if $mbid;
$artist->SetId($id) if $id;
$artist->LoadFromId()
	or return $m->comp(
		"/comp/error",
		"Artist not found in the database.",
		1, 1,
	);

$m->comp(
	"/comp/sidebar",
	pagetitle => "Subscriptions for artist: " . $artist->GetName(),
	title => '',
);

$m->comp("/comp/artisttitle", artist=>$artist);

my @uids = $artist->GetSubscribers;

my $my_uid = $session{uid} || 0;
my @public;
my $private = 0;

for my $uid (@uids)
{
	my $user = UserStuff->newFromId($mb->{DBH}, $uid)
		or next;

	my $is_me = ($uid == $my_uid);
	my $is_public = ($is_me or UserPreference::get_for_user("subscriptions_public", $user));

	if ($is_public)
	{
		my $n = MusicBrainz::NormaliseSortText($user->GetName);
		push @public, [$user->GetId, $user->GetName, $n];
	}
	else
	{
		++$private;
	}
}

@public = sort { $a->[2] cmp $b->[2] } @public;

my $private_str = sprintf "%d anonymous subscriber%s",
	$private, $private==1 ? "" : "s";

</%perl>

	<& /comp/tablebegin, title=>'Editors subscribed to this artist', gattrs => "width='100%'" &>
	<table width="100%" cellspacing="0" cellpadding="0">
		<tr>
			<td style="padding: 0px 4px">
				<%perl>
					if (@public)
					{
						my $i = 0;
						for (@public)
						{
							my ($uid, $name) = @$_;
							$m->out(", ") if $i;
							$m->out(qq|<a href="/user/view.html?uid=$uid">$name</a>|);
							++$i;
						}
						$m->out(" plus $private_str") if $private;
						$m->out(".");
					}
					elsif ($private)
					{
						$m->out($private_str);
					}
					else
					{
						$m->out("none");
					}
				</%perl>
			</td>
		</tr>
		<tr>
			<td align="center">
				&nbsp;<br/>
				<a href="/user/subscribe.html?artistid=<% $artist->GetId %>"
					>Subscribe to this artist</a> |
				<a href="/user/subscribe.html?artistid=<% $artist->GetId %>&unsubscribe=1"
					>Unsubscribe from this artist</a>

			</td>
		</tr>
	</table>
	<& /comp/tableend &>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows an user to modify its subscriptions.
	#
	# $Id$
	#
</%perl>
<%args>

	@artistid => ()
	@labelid => ()
	@editorid => ()
	$unsubscribe => 0
	$returnto => undef

</%args>
<%perl>

		
	if (not @artistid and not @labelid and not @editorid)
	{
		if (defined $returnto)
		{
			return $m->comp("/comp/redirect",
				$returnto,
			);
		}
		else
		{
			return $m->comp("/comp/redirect",
				"/user/subscriptions.html",
			);
		}
	}

	$m->comp(
		"/comp/checkloggedin", 1, 1,
		"You must log in to change your subscriptions.",
	) or return;

	my $mb = $m->comp("/comp/dblogin");
	my @artists;
	my @labels;
	my @editors;

	for my $artistid (@artistid)
	{
		MusicBrainz::Server::Validation::IsNonNegInteger($artistid)
			and $artistid
			and $artistid != &ModDefs::VARTIST_ID
			and $artistid != &ModDefs::DARTIST_ID
			or next;

		my $artist = MusicBrainz::Server::Artist->new($mb->{DBH});
		$artist->SetId($artistid);
		$artist->LoadFromId
			or next;

		push @artists, $artist;
	}

	for my $labelid (@labelid)
	{
		MusicBrainz::Server::Validation::IsNonNegInteger($labelid)
			and $labelid
			and $labelid != &ModDefs::DLABEL_ID
			or next;

		my $label = MusicBrainz::Server::Label->new($mb->{DBH});
		$label->SetId($labelid);
		$label->LoadFromId
			or next;

		push @labels, $label;
	}

	for my $editorid (@editorid)
	{
		MusicBrainz::Server::Validation::IsNonNegInteger($editorid)
			and $editorid
			or next;

		my $ui = UserStuff->new($mb->{DBH});
		my $editor = $ui->newFromId($editorid)
			or next;

		not $editor->IsSpecialEditor
			or next;

		push @editors, $editor;
	}

	(@artists or @labels or @editors)
		or return $m->comp("/comp/badargs", 1, 1);

	my $subs = UserSubscription->new($mb->{DBH});
	$subs->SetUser($session{uid});

	my $method = ($unsubscribe ? "Unsubscribe" : "Subscribe");
	if (@artists)
	{
		my $artistmethod = $method . "Artists";
		$subs->$artistmethod(@artists)
			or return $m->comp("/comp/error",
				"Sorry, there was an error processing your request.", 1, 1,
			);
	}

	if (@labels)
	{
		my $labelmethod = $method . "Labels";
		$subs->$labelmethod(@labels)
			or return $m->comp("/comp/error",
				"Sorry, there was an error processing your request.", 1, 1,
			);
	}

	if (@editors)
	{
		my $editormethod = $method . "Editors";
		$subs->$editormethod(@editors)
			or return $m->comp("/comp/error",
				"Sorry, there was an error processing your request.", 1, 1,
			);
	}

	# redirect user to subscribed users instead of the list of subscriptions
	# if we came from the artist page.
	if (defined $returnto)
	{
		return $m->comp("/comp/redirect",
			$returnto,
		);
	}
	elsif (scalar(@artistid) == 1)
	{
		return $m->comp("/comp/redirect",
			"/show/artist/subscriptions.html?artist=".$artistid[0],
		);
	}
	elsif (scalar(@labelid) == 1)
	{
		return $m->comp("/comp/redirect",
			"/show/label/subscriptions.html?label=".$labelid[0],
		);
	}
	elsif (scalar(@editorid) == 1)
	{
		return $m->comp("/comp/redirect",
			"/user/subscribers.html?userid=".$editorid[0],
		);
	}
	else
	{
		return $m->comp("/comp/redirect", "/user/subscriptions.html");
	}

</%perl>

%# vi: set ts=4 sw=4 ft=mason :

<%perl>
	# -----------------------------------------------------------------------------
	#                               Musicbrainz.org
	#                        Copyright (c) 2001 Robert Kaye
	# -----------------------------------------------------------------------------
	#  This software is provided "as is", without warranty of any kind, express or
	#  implied, including  but not limited  to the warranties of  merchantability,
	#  fitness for a particular purpose and noninfringement. In no event shall the
	#  authors or  copyright  holders be  liable for any claim,  damages or  other
	#  liability, whether  in an  action of  contract, tort  or otherwise, arising
	#  from,  out of  or in  connection with  the software or  the  use  or  other
	#  dealings in the software.
	#
	#  GPL - The GNU General Public License    http://www.gnu.org/licenses/gpl.txt
	#  Permits anyone the right to use and modify the software without limitations
	#  as long as proper  credits are given  and the original  and modified source
	#  code are included. Requires  that the final product, software derivate from
	#  the original  source or any  software  utilizing a GPL  component, such  as
	#  this, is also licensed under the GPL license.
	# -----------------------------------------------------------------------------
	#
	# Summary:
	# -----------------------------------------------------------------------------
	# This page allows a user to verify their e-mail address.
	#
	# $Id$
	#
</%perl>
<%args>

	$uid => -1
	$userid => undef

	$email => ""
	$time => -1
	$chk => ""

</%args>
<%perl>

	# backwards compability
	$userid ||= $uid;

	my $success = 0;
	my $text = "";

	# cannot insert verified e-mail if the database is in
	# readonly state.
	if (&DBDefs::DB_READ_ONLY)
	{
		$text = &DBDefs::DB_READ_ONLY_MESSAGE;
	}
	else
	{
		# make sure we got a valid userid parameter
		MusicBrainz::IsNonNegInteger($userid) && $userid
			or return $m->comp("/comp/layout/badarguments",
				text => "The argument <strong>userid</strong> is missing or has a wrong format.");

		# make sure we got a valid time parameter
		MusicBrainz::IsNonNegInteger($time) && $time
			or return $m->comp("/comp/layout/badarguments",
				text => "The argument <strong>time</strong> is missing or has a wrong format.");

		# make sure we got a valid email parameter
		$email
			or return $m->comp("/comp/layout/badarguments",
				text => "The argument <strong>email</strong> is missing or has a wrong format.");

		# make sure we got a valid email parameter
		$chk
			or return $m->comp("/comp/layout/badarguments",
				text => "The argument <strong>chk</strong> is missing or has a wrong format.");

		# Instantiate MusicBrainz object
		my $mb = $m->comp("/comp/dblogin");

		# load checksum from given parameters
		my $ui = UserStuff->new($mb->{DBH});
		$ui->GetVerifyChecksum($email, $userid, $time) eq $chk
			or return $m->comp("/comp/layout/badarguments",
				text => "The checksum does not match the given arguments.");

		# check if the time limit has expired.
		if ($time + &DBDefs::EMAIL_VERIFICATION_TIMEOUT() < time())
		{
			$text = qq!This verification link has expired. Please
					   <a href="/login.html">Log in</a> and request another
					   verification be sent to your e-mail address.!;
		}
		else
		{
			my $me = $ui->newFromId($userid);
			if (not $me)
			{
				$text = qq!Cannot find user with <strong>userid</strong> $userid.!;

			}
			elsif ($me->SetUserInfo(email => $email))
			{
				$me->SetSession;

				$success = 1;
				$text = qq!Your new e-mail address has been saved. Thank you\!
						<br/>
						<br/>
						[ <a href="/user/view.html">Click here to continue</a> ]!;

			}
			else
			{
				$text = qq!Failed to update user information.!;
			}
 		}
	}

	my $title = $success
		? "E-mail address verified"
		: "Cannot verify e-mail address";

</%perl>

<& /comp/sidebar-notitle, pagetitle => $title &>

	<& /comp/tablebegin, title => $title &>

		<% $text |n %>

	<& /comp/tableend &>

<& /comp/footer &>

%# vi: set ts=4 sw=4 ft=mason :
